module Control.Monad.Free.Quotiented where

open import Prelude
open import Data.List hiding (map)
open import Data.Fin.Sigma
open import Algebra
open import Cubical.Foundations.HLevels using (isSetÎ )

--------------------------------------------------------------------------------
-- Some functors
--------------------------------------------------------------------------------

private variable
  F : Type a â†’ Type a
  G : Type b â†’ Type b

--------------------------------------------------------------------------------
-- A free monad without any quotients: this can be treated as the "syntax tree"
-- for the later proper free monad.
--------------------------------------------------------------------------------

data Syntax (F : Type a â†’ Type a) (A : Type a) : Type (â„“suc a) where
  lift   : (Fx : F A) â†’ Syntax F A
  return : (x  : A) â†’ Syntax F A
  _>>=_  : (xs : Syntax F B) â†’ (k : B â†’ Syntax F A) â†’ Syntax F A

module RawMonadSyntax where
  _>>_ : Syntax F A â†’ Syntax F B â†’ Syntax F B
  xs >> ys = xs >>= const ys

--------------------------------------------------------------------------------
-- Quotients for functors
--------------------------------------------------------------------------------

-- All of these quotients are defined on syntax trees, since otherwise we get a
-- cyclic dependency.
record Equation (Î£ : Type a â†’ Type a) (Î½ : Type a) : Type (â„“suc a) where
  constructor _âŠœ_
  field lhs rhs : Syntax Î£ Î½
open Equation public

record Law (F : Type a â†’ Type a) : Type (â„“suc a) where
  constructor _â†¦_â¦‚_
  field
    Î“ : Type a
    Î½ : Type a
    eqn : Î“ â†’ Equation F Î½
open Law public

Theory : (Type a â†’ Type a) â†’ Type (â„“suc a)
Theory Î£ = List (Law Î£)

private variable ğ’¯ : Theory F

Quotiented : Theory F â†’ (âˆ€ {Î½} â†’ Syntax F Î½ â†’ Syntax F Î½ â†’ Type b) â†’ Type _
Quotiented ğ’¯ R =
      (i : Fin (length ğ’¯)) â†’ -- An index into the list of equations
      let Î“ â†¦ Î½ â¦‚ ğ“‰ = ğ’¯ !! i in -- one of the equations in the list
      isSet Î½ â†’ -- I *think* this is needed
      (Î³ : Î“) â†’ -- The environment, basically the needed things for the equation
      R (lhs (ğ“‰ Î³)) (rhs (ğ“‰ Î³))

--------------------------------------------------------------------------------
-- The free monad, quotiented over a theory
--------------------------------------------------------------------------------
mutual
  data Free (F : Type a â†’ Type a) (ğ’¯ : Theory F) : Type a â†’ Type (â„“suc a) where
  -- The first three constructors are the same as the syntax type
    lift   : (Fx : F A) â†’ Free F ğ’¯ A
    return : (x  : A) â†’ Free F ğ’¯ A
    _>>=_  : (xs : Free F ğ’¯ B) â†’ (k : B â†’ Free F ğ’¯ A) â†’ Free F ğ’¯ A

  -- The quotients for the monad laws
  -- Each of these also takes an isSet parameter: that's the only way I was able
  -- to get it to work!

    >>=-idË¡   : isSet A â†’ (f : B â†’ Free F ğ’¯ A) (x : B) â†’ (return x >>= f) â‰¡ f x
    >>=-idÊ³   : isSet A â†’ (x : Free F ğ’¯ A) â†’ (x >>= return) â‰¡ x
    >>=-assoc : isSet A â†’
                (xs : Free F ğ’¯ C) (f : C â†’ Free F ğ’¯ B) (g : B â†’ Free F ğ’¯ A) â†’
                ((xs >>= f) >>= g) â‰¡ (xs >>= (Î» x â†’ f x >>= g))

  -- Truncation: you wouldn't need this on a normal free monad, but I think it's
  -- theoretically sound and necessary.
    trunc : isSet A â†’ isSet (Free F ğ’¯ A)

  -- This is the quotient for the theory.
    quot : Quotiented ğ’¯ (Î» lhs rhs â†’ âˆ£ lhs âˆ£â†‘ â‰¡ âˆ£ rhs âˆ£â†‘)
     
  -- This converts from the unquotiented syntax to the free type.
  -- You cannot go the other way!
  -- The fact that we're doing all this conversion is what makes things trickier
  -- later (and also that this is interleaved with the data definition).
  âˆ£_âˆ£â†‘ : Syntax F A â†’ Free F ğ’¯ A
  âˆ£ lift x   âˆ£â†‘ = lift x
  âˆ£ return x âˆ£â†‘ = return x
  âˆ£ xs >>= k âˆ£â†‘ = âˆ£ xs âˆ£â†‘ >>= (âˆ£_âˆ£â†‘ âˆ˜ k)

module FreeMonadSyntax where
  _>>_ : Free F ğ’¯ A â†’ Free F ğ’¯ B â†’ Free F ğ’¯ B
  xs >> ys = xs >>= const ys

--------------------------------------------------------------------------------
-- Recursion Schemes
--------------------------------------------------------------------------------

private variable
  p : Level
  P : âˆ€ T â†’ Free F ğ’¯ T â†’ Type p

-- The functor for a free monad (not really! This is a "raw" free functor)
data FreeF (F : Type a â†’ Type a)
           (ğ’¯ : Theory F)
           (P : âˆ€ T â†’ Free F ğ’¯ T â†’ Type b)
           (A : Type a) : Type (â„“suc a â„“âŠ” b) where
  liftF : (Fx : F A) â†’ FreeF F ğ’¯ P A
  returnF : (x : A) â†’ FreeF F ğ’¯ P A
  bindF : (xs : Free F ğ’¯ B)
          (PâŸ¨xsâŸ© : P _ xs)
          (k : B â†’ Free F ğ’¯ A)
          (PâŸ¨âˆ˜kâŸ© : âˆ€ x â†’ P _ (k x)) â†’ FreeF F ğ’¯ P A

-- There can also be a quotiented free functor (I think)

-- The "in" function
âŸª_âŸ« : FreeF F ğ’¯ P A â†’ Free F ğ’¯ A
âŸª liftF x âŸ« = lift x
âŸª returnF x âŸ« = return x
âŸª bindF xs PâŸ¨xsâŸ© k PâŸ¨âˆ˜kâŸ© âŸ« = xs >>= k

-- An algebra
Alg : (F : Type a â†’ Type a) â†’ (ğ’¯ : Theory F) â†’ (P : âˆ€ T â†’ Free F ğ’¯ T â†’ Type b) â†’ Type _
Alg F ğ’¯ P = âˆ€ {A} â†’ (xs : FreeF F ğ’¯ P A) â†’ P A âŸª xs âŸ«

-- You can run a non-coherent algebra on a syntax tree
âŸ¦_âŸ§â†‘ : Alg F ğ’¯ P â†’ (xs : Syntax F A) â†’ P A âˆ£ xs âˆ£â†‘
âŸ¦ alg âŸ§â†‘ (lift x) = alg (liftF x)
âŸ¦ alg âŸ§â†‘ (return x) = alg (returnF x)
âŸ¦ alg âŸ§â†‘ (xs >>= k) = alg (bindF âˆ£ xs âˆ£â†‘ (âŸ¦ alg âŸ§â†‘ xs) (âˆ£_âˆ£â†‘ âˆ˜ k) (âŸ¦ alg âŸ§â†‘ âˆ˜ k))

-- Coherency for an algebra: an algebra that's coherent can be run on a proper
-- Free monad.
record Coherent {a p}
                {F : Type a â†’ Type a}
                {ğ’¯ : Theory F}
                {P : âˆ€ T â†’ Free F ğ’¯ T â†’ Type p}
                (Ïˆ : Alg F ğ’¯ P) : Type (â„“suc a â„“âŠ” p) where
  field
    c-set : âˆ€ {T} â†’ isSet T â†’ âˆ€ xs â†’ isSet (P T xs)

    c->>=idË¡ : âˆ€ (isb : isSet B) (f : A â†’ Free F ğ’¯ B) Pf x â†’
      Ïˆ (bindF (return x) (Ïˆ (returnF x)) f Pf)
        â‰¡[ i â‰” P _ (>>=-idË¡ isb f x i) ]â‰¡ Pf x

    c->>=idÊ³ : âˆ€ (isa : isSet A) (x : Free F ğ’¯ A) Px â†’
      Ïˆ (bindF x Px return (Î» y â†’ Ïˆ (returnF y)))
        â‰¡[ i â‰” P A (>>=-idÊ³ isa x i) ]â‰¡ Px

    c->>=assoc : âˆ€ (isa : isSet A)
      (xs : Free F ğ’¯ C) Pxs
      (f : C â†’ Free F ğ’¯ B) Pf
      (g : B â†’ Free F ğ’¯ A) Pg â†’
      Ïˆ (bindF (xs >>= f) (Ïˆ (bindF xs Pxs f Pf)) g Pg)
        â‰¡[ i â‰” P A (>>=-assoc isa xs f g i) ]â‰¡
          Ïˆ (bindF xs Pxs (Î» x â†’ f x >>= g) Î» x â†’ Ïˆ (bindF (f x) (Pf x) g Pg))

    c-quot : (i : Fin (length ğ’¯)) â†’
             let Î“ â†¦ Î½ â¦‚ ğ“‰ = ğ’¯ !! i in
             (iss : isSet Î½) â†’
             (Î³ : Î“) â†’
             âŸ¦ Ïˆ âŸ§â†‘ (lhs (ğ“‰ Î³)) â‰¡[ j â‰” P Î½ (quot i iss Î³ j) ]â‰¡ âŸ¦ Ïˆ âŸ§â†‘ (rhs (ğ“‰ Î³))
open Coherent public

-- A full dependent algebra
Î¨ : (F : Type a â†’ Type a) (ğ’¯ : Theory F) (P : âˆ€ T â†’ Free F ğ’¯ T â†’ Type p) â†’ Type _
Î¨ F ğ’¯ P = Î£ (Alg F ğ’¯ P) Coherent

Î¨-syntax : (F : Type a â†’ Type a) (ğ’¯ : Theory F) (P : âˆ€ {T} â†’ Free F ğ’¯ T â†’ Type p) â†’ Type _
Î¨-syntax F ğ’¯ P = Î¨ F ğ’¯ (Î» T â†’ P {T})

syntax Î¨-syntax F ğ’¯ (Î» xs â†’ P) = Î¨[ xs â¦‚ F â‹† * / ğ’¯ ] â‡’ P

-- Non-dependent algebras
Î¦ : (F : Type a â†’ Type a) â†’ (ğ’¯ : Theory F) â†’ (Type a â†’ Type b) â†’ Type _
Î¦ A ğ’¯ B = Î¨ A ğ’¯ (Î» T _ â†’ B T)

syntax Î¦ F ğ’¯ (Î» A â†’ B) = Î¦[ F â‹† A / ğ’¯ ] â‡’ B

-- Running the algebra
module _ (Ïˆ : Î¨ F ğ’¯ P) where
  âŸ¦_âŸ§ : (xs : Free F ğ’¯ A) â†’ P A xs

  -- We need the terminating pragma here because Agda can't see that âˆ£_âˆ£â†‘ makes
  -- something the same size (I think)
  {-# TERMINATING #-}
  undecorate : (xs : Syntax F A) â†’ âŸ¦ fst Ïˆ âŸ§â†‘ xs â‰¡ âŸ¦ âˆ£ xs âˆ£â†‘ âŸ§
  undecorate (lift x) i = fst Ïˆ (liftF x)
  undecorate (return x) i = fst Ïˆ (returnF x)
  undecorate (xs >>= k) i =
    fst Ïˆ
        (bindF âˆ£ xs âˆ£â†‘ (undecorate xs i) (Î» x â†’ âˆ£ k x âˆ£â†‘)
        (Î» x â†’ undecorate (k x) i))

  âŸ¦ lift x âŸ§ = Ïˆ .fst (liftF x)
  âŸ¦ return x âŸ§ = Ïˆ .fst (returnF x)
  âŸ¦ xs >>= k âŸ§ = Ïˆ .fst (bindF xs âŸ¦ xs âŸ§ k (âŸ¦_âŸ§ âˆ˜ k))

  âŸ¦ >>=-idË¡ iss f k i âŸ§ = Ïˆ .snd .c->>=idË¡ iss f (âŸ¦_âŸ§ âˆ˜ f) k i
  âŸ¦ >>=-idÊ³ iss xs i âŸ§ = Ïˆ .snd .c->>=idÊ³ iss xs âŸ¦ xs âŸ§ i
  âŸ¦ >>=-assoc iss xs f g i âŸ§ =
    Ïˆ .snd .c->>=assoc iss xs âŸ¦ xs âŸ§ f (âŸ¦_âŸ§ âˆ˜ f) g (âŸ¦_âŸ§ âˆ˜ g) i

  âŸ¦ quot p iss e i âŸ§ =
      substâ‚‚ (PathP (Î» j â†’ P _ (quot p iss e j)))
              (undecorate _)
              (undecorate _)
              (Ïˆ .snd .c-quot p iss e) i

  âŸ¦ trunc AIsSet xs ys p q i j âŸ§ =
    isOfHLevelâ†’isOfHLevelDep 2
      (Ïˆ .snd .c-set AIsSet)
      âŸ¦ xs âŸ§ âŸ¦ ys âŸ§
      (cong âŸ¦_âŸ§ p) (cong âŸ¦_âŸ§ q)
      (trunc AIsSet xs ys p q)
      i j

-- For a proposition, use this to prove the algebra is coherent
prop-coh : {alg : Alg F ğ’¯ P} â†’ (âˆ€ {T} â†’ isSet T â†’ âˆ€ xs â†’ isProp (P T xs)) â†’ Coherent alg
prop-coh P-isProp .c-set TIsSet xs = isPropâ†’isSet (P-isProp TIsSet xs)
prop-coh {P = P} P-isProp .c->>=idË¡ iss f Pf x =
  toPathP (P-isProp iss (f x) (transp (Î» i â†’ P _ (>>=-idË¡ iss f x i)) i0 _) _)
prop-coh {P = P} P-isProp .c->>=idÊ³ iss x Px =
  toPathP (P-isProp iss x (transp (Î» i â†’ P _ (>>=-idÊ³ iss x i)) i0 _) _)
prop-coh {P = P} P-isProp .c->>=assoc iss xs Pxs f Pf g Pg =
  toPathP (P-isProp iss (xs >>= (Î» x â†’ f x >>= g)) (transp (Î» i â†’ P _ (>>=-assoc iss xs f g i)) i0 _) _)
prop-coh {ğ’¯ = ğ’¯} {P = P} P-isProp .c-quot i iss e =
  toPathP (P-isProp iss (âˆ£ (ğ’¯ !! i) .eqn e .rhs âˆ£â†‘) (transp (Î» j â†’ P _ (quot i iss e j)) i0 _) _)

-- A more conventional catamorphism
module _ {â„“} (fun : Functor â„“ â„“) where
  open Functor fun using (map; ğ¹)
  module _ {B : Type â„“} {ğ’¯ : Theory ğ¹} (BIsSet : isSet B) where
    module _ (Ï• : ğ¹ B â†’ B) where
      Ï•â‚ : Alg ğ¹ ğ’¯ Î» T _ â†’ (T â†’ B) â†’ B
      Ï•â‚ (liftF x) h = Ï• (map h x)
      Ï•â‚ (returnF x) h = h x
      Ï•â‚ (bindF _ PâŸ¨xsâŸ© _ PâŸ¨âˆ˜kâŸ©) h = PâŸ¨xsâŸ© (flip PâŸ¨âˆ˜kâŸ© h)

      InTheory : Type _
      InTheory = Quotiented ğ’¯ Î» lhs rhs â†’ âˆ€ f â†’ âŸ¦ Ï•â‚ âŸ§â†‘ lhs f â‰¡ âŸ¦ Ï•â‚ âŸ§â†‘ rhs f

      module _ (Ï•-coh : InTheory) where
        cata-coh : Coherent Ï•â‚
        cata-coh .c-set _ _ = isSetÎ  Î» _ â†’ BIsSet
        cata-coh .c->>=idË¡ isb f Pf x = refl
        cata-coh .c->>=idÊ³ isa x Px = refl
        cata-coh .c->>=assoc isa xs Pxs f Pf g Pg = refl
        cata-coh .c-quot i iss e j f = Ï•-coh i iss e f j

        cata-alg : Î¦[ ğ¹ â‹† A / ğ’¯ ] â‡’ ((A â†’ B) â†’ B)
        cata-alg = Ï•â‚ , cata-coh

    cata : (A â†’ B) â†’ (Ï• : ğ¹ B â†’ B) â†’ InTheory Ï• â†’ Free ğ¹ ğ’¯ A â†’ B
    cata h Ï• coh xs = âŸ¦ cata-alg Ï• coh âŸ§ xs h

