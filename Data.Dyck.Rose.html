<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Dyck.Rose</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--cubical</a> <a id="23" class="Pragma">--safe</a> <a id="30" class="Pragma">--postfix-projections</a> <a id="52" class="Symbol">#-}</a>

<a id="57" class="Comment">-- This module defines a data type for balanced strings of parentheses,</a>
<a id="129" class="Comment">-- which is isomorphic to binary trees.</a>

<a id="170" class="Keyword">module</a> <a id="177" href="Data.Dyck.Rose.html" class="Module">Data.Dyck.Rose</a> <a id="192" class="Keyword">where</a>

<a id="199" class="Keyword">open</a> <a id="204" class="Keyword">import</a> <a id="211" href="Prelude.html" class="Module">Prelude</a>
<a id="219" class="Keyword">open</a> <a id="224" class="Keyword">import</a> <a id="231" href="Data.Nat.html" class="Module">Data.Nat</a> <a id="240" class="Keyword">using</a> <a id="246" class="Symbol">(</a><a id="247" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">_+_</a><a id="250" class="Symbol">)</a>
<a id="252" class="Keyword">open</a> <a id="257" class="Keyword">import</a> <a id="264" href="Data.Vec.Iterated.html" class="Module">Data.Vec.Iterated</a> <a id="282" class="Keyword">using</a> <a id="288" class="Symbol">(</a><a id="289" href="Data.Vec.Iterated.html#218" class="Function">Vec</a><a id="292" class="Symbol">;</a> <a id="294" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">_∷_</a><a id="297" class="Symbol">;</a> <a id="299" href="Data.Vec.Iterated.html#347" class="InductiveConstructor">[]</a><a id="301" class="Symbol">;</a> <a id="303" href="Data.Vec.Iterated.html#961" class="Function">foldlN</a><a id="309" class="Symbol">;</a> <a id="311" href="Data.Vec.Iterated.html#487" class="Field">head</a><a id="315" class="Symbol">)</a>
<a id="317" class="Keyword">open</a> <a id="322" class="Keyword">import</a> <a id="329" href="Data.Tree.Rose.html" class="Module">Data.Tree.Rose</a>
<a id="344" class="Keyword">open</a> <a id="349" class="Keyword">import</a> <a id="356" href="Data.List.html" class="Module">Data.List</a>

<a id="367" class="Keyword">private</a>
  <a id="377" class="Keyword">variable</a>
    <a id="390" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a> <a id="392" class="Symbol">:</a> <a id="394" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a>

<a id="397" class="Comment">--------------------------------------------------------------------------------</a>
<a id="478" class="Comment">-- Programs: definition and associated functions</a>
<a id="527" class="Comment">--------------------------------------------------------------------------------</a>

<a id="609" class="Keyword">data</a> <a id="Prog"></a><a id="614" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="619" class="Symbol">(</a><a id="620" href="Data.Dyck.Rose.html#620" class="Bound">A</a> <a id="622" class="Symbol">:</a> <a id="624" href="Level.html#192" class="Function">Type</a> <a id="629" href="Level.html#372" class="Generalizable">a</a><a id="630" class="Symbol">)</a> <a id="632" class="Symbol">:</a> <a id="634" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="636" class="Symbol">→</a> <a id="638" href="Level.html#192" class="Function">Type</a> <a id="643" href="Data.Dyck.Rose.html#629" class="Bound">a</a> <a id="645" class="Keyword">where</a>
  <a id="Prog.halt"></a><a id="653" href="Data.Dyck.Rose.html#653" class="InductiveConstructor">halt</a> <a id="658" class="Symbol">:</a> <a id="660" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="665" href="Data.Dyck.Rose.html#620" class="Bound">A</a> <a id="667" class="Number">1</a>
  <a id="Prog.pull"></a><a id="671" href="Data.Dyck.Rose.html#671" class="InductiveConstructor">pull</a> <a id="676" class="Symbol">:</a> <a id="678" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="683" href="Data.Dyck.Rose.html#620" class="Bound">A</a> <a id="685" class="Symbol">(</a><a id="686" class="Number">1</a> <a id="688" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">+</a> <a id="690" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a><a id="691" class="Symbol">)</a> <a id="693" class="Symbol">→</a> <a id="695" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="700" href="Data.Dyck.Rose.html#620" class="Bound">A</a> <a id="702" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a>
  <a id="Prog.push"></a><a id="706" href="Data.Dyck.Rose.html#706" class="InductiveConstructor">push</a> <a id="711" class="Symbol">:</a> <a id="713" href="Data.Dyck.Rose.html#620" class="Bound">A</a> <a id="715" class="Symbol">→</a> <a id="717" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="722" href="Data.Dyck.Rose.html#620" class="Bound">A</a> <a id="724" class="Symbol">(</a><a id="725" class="Number">1</a> <a id="727" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">+</a> <a id="729" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a><a id="730" class="Symbol">)</a> <a id="732" class="Symbol">→</a> <a id="734" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="739" href="Data.Dyck.Rose.html#620" class="Bound">A</a> <a id="741" class="Symbol">(</a><a id="742" class="Number">2</a> <a id="744" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">+</a> <a id="746" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a><a id="747" class="Symbol">)</a>

<a id="750" class="Comment">--------------------------------------------------------------------------------</a>
<a id="831" class="Comment">-- Conversion from a Prog to a Tree</a>
<a id="867" class="Comment">--------------------------------------------------------------------------------</a>

<a id="prog→tree⊙"></a><a id="949" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="960" class="Symbol">:</a> <a id="962" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="967" href="Level.html#388" class="Generalizable">A</a> <a id="969" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a> <a id="971" class="Symbol">→</a> <a id="973" href="Data.Vec.Iterated.html#218" class="Function">Vec</a> <a id="977" class="Symbol">(</a><a id="978" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="985" href="Level.html#388" class="Generalizable">A</a><a id="986" class="Symbol">)</a> <a id="988" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a> <a id="990" class="Symbol">→</a> <a id="992" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="999" href="Level.html#388" class="Generalizable">A</a>
<a id="1001" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="1012" href="Data.Dyck.Rose.html#653" class="InductiveConstructor">halt</a>        <a id="1024" class="Symbol">(</a><a id="1025" href="Data.Dyck.Rose.html#1025" class="Bound">v</a> <a id="1027" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="1029" href="Data.Vec.Iterated.html#347" class="InductiveConstructor">[]</a><a id="1031" class="Symbol">)</a>       <a id="1039" class="Symbol">=</a> <a id="1041" href="Data.Dyck.Rose.html#1025" class="Bound">v</a>
<a id="1043" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="1054" class="Symbol">(</a><a id="1055" href="Data.Dyck.Rose.html#671" class="InductiveConstructor">pull</a>   <a id="1062" href="Data.Dyck.Rose.html#1062" class="Bound">is</a><a id="1064" class="Symbol">)</a> <a id="1066" href="Data.Dyck.Rose.html#1066" class="Bound">st</a>             <a id="1081" class="Symbol">=</a> <a id="1083" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="1094" href="Data.Dyck.Rose.html#1062" class="Bound">is</a> <a id="1097" class="Symbol">(</a><a id="1098" href="Agda.Builtin.List.html#185" class="InductiveConstructor">[]</a> <a id="1101" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="1103" href="Data.Dyck.Rose.html#1066" class="Bound">st</a><a id="1105" class="Symbol">)</a>
<a id="1107" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="1118" class="Symbol">(</a><a id="1119" href="Data.Dyck.Rose.html#706" class="InductiveConstructor">push</a> <a id="1124" href="Data.Dyck.Rose.html#1124" class="Bound">v</a> <a id="1126" href="Data.Dyck.Rose.html#1126" class="Bound">is</a><a id="1128" class="Symbol">)</a> <a id="1130" class="Symbol">(</a><a id="1131" href="Data.Dyck.Rose.html#1131" class="Bound">t₁</a> <a id="1134" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="1136" href="Data.Dyck.Rose.html#1136" class="Bound">t₂</a> <a id="1139" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="1141" href="Data.Dyck.Rose.html#1141" class="Bound">st</a><a id="1143" class="Symbol">)</a> <a id="1145" class="Symbol">=</a> <a id="1147" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="1158" href="Data.Dyck.Rose.html#1126" class="Bound">is</a> <a id="1161" class="Symbol">(((</a><a id="1164" href="Data.Dyck.Rose.html#1124" class="Bound">v</a> <a id="1166" href="Data.Tree.Rose.html#261" class="InductiveConstructor Operator">&amp;</a> <a id="1168" href="Data.Dyck.Rose.html#1136" class="Bound">t₂</a><a id="1170" class="Symbol">)</a> <a id="1172" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="1174" href="Data.Dyck.Rose.html#1131" class="Bound">t₁</a><a id="1176" class="Symbol">)</a> <a id="1178" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="1180" href="Data.Dyck.Rose.html#1141" class="Bound">st</a><a id="1182" class="Symbol">)</a>

<a id="prog→tree"></a><a id="1185" href="Data.Dyck.Rose.html#1185" class="Function">prog→tree</a> <a id="1195" class="Symbol">:</a> <a id="1197" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="1202" href="Level.html#388" class="Generalizable">A</a> <a id="1204" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a> <a id="1209" class="Symbol">→</a> <a id="1211" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="1218" href="Level.html#388" class="Generalizable">A</a>
<a id="1220" href="Data.Dyck.Rose.html#1185" class="Function">prog→tree</a> <a id="1230" href="Data.Dyck.Rose.html#1230" class="Bound">ds</a> <a id="1233" class="Symbol">=</a> <a id="1235" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="1246" href="Data.Dyck.Rose.html#1230" class="Bound">ds</a> <a id="1249" href="Data.Vec.Iterated.html#347" class="InductiveConstructor">[]</a>

<a id="1253" class="Comment">--------------------------------------------------------------------------------</a>
<a id="1334" class="Comment">-- Conversion from a Tree to a Prog</a>
<a id="1370" class="Comment">--------------------------------------------------------------------------------</a>

<a id="tree→prog⊙"></a><a id="1452" href="Data.Dyck.Rose.html#1452" class="Function">tree→prog⊙</a> <a id="1463" class="Symbol">:</a> <a id="1465" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="1472" href="Level.html#388" class="Generalizable">A</a> <a id="1474" class="Symbol">→</a> <a id="1476" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="1481" href="Level.html#388" class="Generalizable">A</a> <a id="1483" class="Symbol">(</a><a id="1484" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1488" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a><a id="1489" class="Symbol">)</a> <a id="1491" class="Symbol">→</a> <a id="1493" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="1498" href="Level.html#388" class="Generalizable">A</a> <a id="1500" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a>
<a id="1502" href="Data.Dyck.Rose.html#1452" class="Function">tree→prog⊙</a> <a id="1513" href="Agda.Builtin.List.html#185" class="InductiveConstructor">[]</a>              <a id="1529" class="Symbol">=</a> <a id="1531" href="Data.Dyck.Rose.html#671" class="InductiveConstructor">pull</a>
<a id="1536" href="Data.Dyck.Rose.html#1452" class="Function">tree→prog⊙</a> <a id="1547" class="Symbol">((</a><a id="1549" href="Data.Dyck.Rose.html#1549" class="Bound">t</a> <a id="1551" href="Data.Tree.Rose.html#261" class="InductiveConstructor Operator">&amp;</a> <a id="1553" href="Data.Dyck.Rose.html#1553" class="Bound">ts</a><a id="1555" class="Symbol">)</a> <a id="1557" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="1559" href="Data.Dyck.Rose.html#1559" class="Bound">xs</a><a id="1561" class="Symbol">)</a> <a id="1563" class="Symbol">=</a> <a id="1565" href="Data.Dyck.Rose.html#1452" class="Function">tree→prog⊙</a> <a id="1576" href="Data.Dyck.Rose.html#1553" class="Bound">ts</a> <a id="1579" href="Function.html#97" class="Function Operator">∘</a> <a id="1581" href="Data.Dyck.Rose.html#1452" class="Function">tree→prog⊙</a> <a id="1592" href="Data.Dyck.Rose.html#1559" class="Bound">xs</a> <a id="1595" href="Function.html#97" class="Function Operator">∘</a> <a id="1597" href="Data.Dyck.Rose.html#706" class="InductiveConstructor">push</a> <a id="1602" href="Data.Dyck.Rose.html#1549" class="Bound">t</a>

<a id="tree→prog"></a><a id="1605" href="Data.Dyck.Rose.html#1605" class="Function">tree→prog</a> <a id="1615" class="Symbol">:</a> <a id="1617" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="1624" href="Level.html#388" class="Generalizable">A</a> <a id="1626" class="Symbol">→</a> <a id="1628" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="1633" href="Level.html#388" class="Generalizable">A</a> <a id="1635" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a>
<a id="1640" href="Data.Dyck.Rose.html#1605" class="Function">tree→prog</a> <a id="1650" href="Data.Dyck.Rose.html#1650" class="Bound">tr</a> <a id="1653" class="Symbol">=</a> <a id="1655" href="Data.Dyck.Rose.html#1452" class="Function">tree→prog⊙</a> <a id="1666" href="Data.Dyck.Rose.html#1650" class="Bound">tr</a> <a id="1669" href="Data.Dyck.Rose.html#653" class="InductiveConstructor">halt</a>

<a id="1675" class="Comment">--------------------------------------------------------------------------------</a>
<a id="1756" class="Comment">-- Proof of isomorphism</a>
<a id="1780" class="Comment">--------------------------------------------------------------------------------</a>

<a id="tree→prog→tree⊙"></a><a id="1862" href="Data.Dyck.Rose.html#1862" class="Function">tree→prog→tree⊙</a> <a id="1878" class="Symbol">:</a> <a id="1880" class="Symbol">(</a><a id="1881" href="Data.Dyck.Rose.html#1881" class="Bound">e</a> <a id="1883" class="Symbol">:</a> <a id="1885" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="1892" href="Level.html#388" class="Generalizable">A</a><a id="1893" class="Symbol">)</a> <a id="1895" class="Symbol">(</a><a id="1896" href="Data.Dyck.Rose.html#1896" class="Bound">is</a> <a id="1899" class="Symbol">:</a> <a id="1901" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="1906" href="Level.html#388" class="Generalizable">A</a> <a id="1908" class="Symbol">(</a><a id="1909" class="Number">1</a> <a id="1911" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">+</a> <a id="1913" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a><a id="1914" class="Symbol">))</a> <a id="1917" class="Symbol">(</a><a id="1918" href="Data.Dyck.Rose.html#1918" class="Bound">st</a> <a id="1921" class="Symbol">:</a> <a id="1923" href="Data.Vec.Iterated.html#218" class="Function">Vec</a> <a id="1927" class="Symbol">(</a><a id="1928" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="1935" href="Level.html#388" class="Generalizable">A</a><a id="1936" class="Symbol">)</a> <a id="1938" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a><a id="1939" class="Symbol">)</a> <a id="1941" class="Symbol">→</a>
  <a id="1945" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="1956" class="Symbol">(</a><a id="1957" href="Data.Dyck.Rose.html#1452" class="Function">tree→prog⊙</a> <a id="1968" href="Data.Dyck.Rose.html#1881" class="Bound">e</a> <a id="1970" href="Data.Dyck.Rose.html#1896" class="Bound">is</a><a id="1972" class="Symbol">)</a> <a id="1974" href="Data.Dyck.Rose.html#1918" class="Bound">st</a> <a id="1977" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1979" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="1990" href="Data.Dyck.Rose.html#1896" class="Bound">is</a> <a id="1993" class="Symbol">(</a><a id="1994" href="Data.Dyck.Rose.html#1881" class="Bound">e</a> <a id="1996" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="1998" href="Data.Dyck.Rose.html#1918" class="Bound">st</a><a id="2000" class="Symbol">)</a>
<a id="2002" href="Data.Dyck.Rose.html#1862" class="Function">tree→prog→tree⊙</a> <a id="2018" href="Agda.Builtin.List.html#185" class="InductiveConstructor">[]</a>              <a id="2034" href="Data.Dyck.Rose.html#2034" class="Bound">is</a> <a id="2037" href="Data.Dyck.Rose.html#2037" class="Bound">st</a> <a id="2040" class="Symbol">=</a> <a id="2042" href="Cubical.Foundations.Id.html#570" class="Function">refl</a>
<a id="2047" href="Data.Dyck.Rose.html#1862" class="Function">tree→prog→tree⊙</a> <a id="2063" class="Symbol">((</a><a id="2065" href="Data.Dyck.Rose.html#2065" class="Bound">t</a> <a id="2067" href="Data.Tree.Rose.html#261" class="InductiveConstructor Operator">&amp;</a> <a id="2069" href="Data.Dyck.Rose.html#2069" class="Bound">ts</a><a id="2071" class="Symbol">)</a> <a id="2073" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="2075" href="Data.Dyck.Rose.html#2075" class="Bound">xs</a><a id="2077" class="Symbol">)</a> <a id="2079" href="Data.Dyck.Rose.html#2079" class="Bound">is</a> <a id="2082" href="Data.Dyck.Rose.html#2082" class="Bound">st</a> <a id="2085" class="Symbol">=</a>
  <a id="2089" href="Data.Dyck.Rose.html#1862" class="Function">tree→prog→tree⊙</a> <a id="2105" href="Data.Dyck.Rose.html#2069" class="Bound">ts</a> <a id="2108" class="Symbol">_</a> <a id="2110" href="Data.Dyck.Rose.html#2082" class="Bound">st</a> <a id="2113" href="Cubical.Foundations.Id.html#747" class="Function Operator">;</a> <a id="2115" href="Data.Dyck.Rose.html#1862" class="Function">tree→prog→tree⊙</a> <a id="2131" href="Data.Dyck.Rose.html#2075" class="Bound">xs</a> <a id="2134" class="Symbol">(</a><a id="2135" href="Data.Dyck.Rose.html#706" class="InductiveConstructor">push</a> <a id="2140" href="Data.Dyck.Rose.html#2065" class="Bound">t</a> <a id="2142" href="Data.Dyck.Rose.html#2079" class="Bound">is</a><a id="2144" class="Symbol">)</a> <a id="2146" class="Symbol">(</a><a id="2147" href="Data.Dyck.Rose.html#2069" class="Bound">ts</a> <a id="2150" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="2152" href="Data.Dyck.Rose.html#2082" class="Bound">st</a><a id="2154" class="Symbol">)</a>

<a id="tree→prog→tree"></a><a id="2157" href="Data.Dyck.Rose.html#2157" class="Function">tree→prog→tree</a> <a id="2172" class="Symbol">:</a> <a id="2174" class="Symbol">(</a><a id="2175" href="Data.Dyck.Rose.html#2175" class="Bound">e</a> <a id="2177" class="Symbol">:</a> <a id="2179" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="2186" href="Level.html#388" class="Generalizable">A</a><a id="2187" class="Symbol">)</a> <a id="2189" class="Symbol">→</a> <a id="2191" href="Data.Dyck.Rose.html#1185" class="Function">prog→tree</a> <a id="2201" class="Symbol">(</a><a id="2202" href="Data.Dyck.Rose.html#1605" class="Function">tree→prog</a> <a id="2212" href="Data.Dyck.Rose.html#2175" class="Bound">e</a><a id="2213" class="Symbol">)</a> <a id="2215" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="2217" href="Data.Dyck.Rose.html#2175" class="Bound">e</a>
<a id="2219" href="Data.Dyck.Rose.html#2157" class="Function">tree→prog→tree</a> <a id="2234" href="Data.Dyck.Rose.html#2234" class="Bound">e</a> <a id="2236" class="Symbol">=</a> <a id="2238" href="Data.Dyck.Rose.html#1862" class="Function">tree→prog→tree⊙</a> <a id="2254" href="Data.Dyck.Rose.html#2234" class="Bound">e</a> <a id="2256" href="Data.Dyck.Rose.html#653" class="InductiveConstructor">halt</a> <a id="2261" href="Data.Vec.Iterated.html#347" class="InductiveConstructor">[]</a>

<a id="prog→tree→prog⊙"></a><a id="2265" href="Data.Dyck.Rose.html#2265" class="Function">prog→tree→prog⊙</a> <a id="2281" class="Symbol">:</a> <a id="2283" class="Symbol">(</a><a id="2284" href="Data.Dyck.Rose.html#2284" class="Bound">is</a> <a id="2287" class="Symbol">:</a> <a id="2289" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="2294" href="Level.html#388" class="Generalizable">A</a> <a id="2296" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a><a id="2297" class="Symbol">)</a> <a id="2299" class="Symbol">(</a><a id="2300" href="Data.Dyck.Rose.html#2300" class="Bound">st</a> <a id="2303" class="Symbol">:</a> <a id="2305" href="Data.Vec.Iterated.html#218" class="Function">Vec</a> <a id="2309" class="Symbol">(</a><a id="2310" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="2317" href="Level.html#388" class="Generalizable">A</a><a id="2318" class="Symbol">)</a> <a id="2320" href="Data.Dyck.Rose.html#390" class="Generalizable">n</a><a id="2321" class="Symbol">)</a> <a id="2323" class="Symbol">→</a>
 <a id="2326" href="Data.Dyck.Rose.html#1605" class="Function">tree→prog</a> <a id="2336" class="Symbol">(</a><a id="2337" href="Data.Dyck.Rose.html#949" class="Function">prog→tree⊙</a> <a id="2348" href="Data.Dyck.Rose.html#2284" class="Bound">is</a> <a id="2351" href="Data.Dyck.Rose.html#2300" class="Bound">st</a><a id="2353" class="Symbol">)</a> <a id="2355" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="2357" href="Data.Vec.Iterated.html#961" class="Function">foldlN</a> <a id="2364" class="Symbol">(</a><a id="2365" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="2370" href="Level.html#388" class="Generalizable">A</a><a id="2371" class="Symbol">)</a> <a id="2373" href="Data.Dyck.Rose.html#1452" class="Function">tree→prog⊙</a> <a id="2384" href="Data.Dyck.Rose.html#2284" class="Bound">is</a> <a id="2387" href="Data.Dyck.Rose.html#2300" class="Bound">st</a>
<a id="2390" href="Data.Dyck.Rose.html#2265" class="Function">prog→tree→prog⊙</a> <a id="2406" href="Data.Dyck.Rose.html#653" class="InductiveConstructor">halt</a>        <a id="2418" href="Data.Dyck.Rose.html#2418" class="Bound">st</a> <a id="2421" class="Symbol">=</a> <a id="2423" href="Cubical.Foundations.Id.html#570" class="Function">refl</a>
<a id="2428" href="Data.Dyck.Rose.html#2265" class="Function">prog→tree→prog⊙</a> <a id="2444" class="Symbol">(</a><a id="2445" href="Data.Dyck.Rose.html#671" class="InductiveConstructor">pull</a>   <a id="2452" href="Data.Dyck.Rose.html#2452" class="Bound">is</a><a id="2454" class="Symbol">)</a> <a id="2456" href="Data.Dyck.Rose.html#2456" class="Bound">st</a> <a id="2459" class="Symbol">=</a> <a id="2461" href="Data.Dyck.Rose.html#2265" class="Function">prog→tree→prog⊙</a> <a id="2477" href="Data.Dyck.Rose.html#2452" class="Bound">is</a> <a id="2480" class="Symbol">(</a><a id="2481" href="Agda.Builtin.List.html#185" class="InductiveConstructor">[]</a> <a id="2484" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="2486" href="Data.Dyck.Rose.html#2456" class="Bound">st</a><a id="2488" class="Symbol">)</a>
<a id="2490" href="Data.Dyck.Rose.html#2265" class="Function">prog→tree→prog⊙</a> <a id="2506" class="Symbol">(</a><a id="2507" href="Data.Dyck.Rose.html#706" class="InductiveConstructor">push</a> <a id="2512" href="Data.Dyck.Rose.html#2512" class="Bound">x</a> <a id="2514" href="Data.Dyck.Rose.html#2514" class="Bound">is</a><a id="2516" class="Symbol">)</a> <a id="2518" class="Symbol">(</a><a id="2519" href="Data.Dyck.Rose.html#2519" class="Bound">t₁</a> <a id="2522" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="2524" href="Data.Dyck.Rose.html#2524" class="Bound">t₂</a> <a id="2527" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="2529" href="Data.Dyck.Rose.html#2529" class="Bound">st</a><a id="2531" class="Symbol">)</a> <a id="2533" class="Symbol">=</a>
  <a id="2537" href="Data.Dyck.Rose.html#2265" class="Function">prog→tree→prog⊙</a> <a id="2553" href="Data.Dyck.Rose.html#2514" class="Bound">is</a> <a id="2556" class="Symbol">(((</a><a id="2559" href="Data.Dyck.Rose.html#2512" class="Bound">x</a> <a id="2561" href="Data.Tree.Rose.html#261" class="InductiveConstructor Operator">&amp;</a> <a id="2563" href="Data.Dyck.Rose.html#2524" class="Bound">t₂</a><a id="2565" class="Symbol">)</a> <a id="2567" href="Agda.Builtin.List.html#200" class="InductiveConstructor Operator">∷</a> <a id="2569" href="Data.Dyck.Rose.html#2519" class="Bound">t₁</a><a id="2571" class="Symbol">)</a> <a id="2573" href="Data.Vec.Iterated.html#467" class="InductiveConstructor Operator">∷</a> <a id="2575" href="Data.Dyck.Rose.html#2529" class="Bound">st</a><a id="2577" class="Symbol">)</a>

<a id="prog→tree→prog"></a><a id="2580" href="Data.Dyck.Rose.html#2580" class="Function">prog→tree→prog</a> <a id="2595" class="Symbol">:</a> <a id="2597" class="Symbol">(</a><a id="2598" href="Data.Dyck.Rose.html#2598" class="Bound">is</a> <a id="2601" class="Symbol">:</a> <a id="2603" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="2608" href="Level.html#388" class="Generalizable">A</a> <a id="2610" class="Number">0</a><a id="2611" class="Symbol">)</a> <a id="2613" class="Symbol">→</a> <a id="2615" href="Data.Dyck.Rose.html#1605" class="Function">tree→prog</a> <a id="2625" class="Symbol">(</a><a id="2626" href="Data.Dyck.Rose.html#1185" class="Function">prog→tree</a> <a id="2636" href="Data.Dyck.Rose.html#2598" class="Bound">is</a><a id="2638" class="Symbol">)</a> <a id="2640" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="2642" href="Data.Dyck.Rose.html#2598" class="Bound">is</a>
<a id="2645" href="Data.Dyck.Rose.html#2580" class="Function">prog→tree→prog</a> <a id="2660" href="Data.Dyck.Rose.html#2660" class="Bound">is</a> <a id="2663" class="Symbol">=</a> <a id="2665" href="Data.Dyck.Rose.html#2265" class="Function">prog→tree→prog⊙</a> <a id="2681" href="Data.Dyck.Rose.html#2660" class="Bound">is</a> <a id="2684" href="Data.Vec.Iterated.html#347" class="InductiveConstructor">[]</a>

<a id="prog-iso"></a><a id="2688" href="Data.Dyck.Rose.html#2688" class="Function">prog-iso</a> <a id="2697" class="Symbol">:</a> <a id="2699" href="Data.Dyck.Rose.html#614" class="Datatype">Prog</a> <a id="2704" href="Level.html#388" class="Generalizable">A</a> <a id="2706" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a> <a id="2711" href="Function.Isomorphism.html#338" class="Function Operator">⇔</a> <a id="2713" href="Data.Tree.Rose.html#117" class="Function">Forest</a> <a id="2720" href="Level.html#388" class="Generalizable">A</a>
<a id="2722" href="Data.Dyck.Rose.html#2688" class="Function">prog-iso</a> <a id="2731" class="Symbol">.</a><a id="2732" href="Cubical.Foundations.Isomorphism.html#810" class="Field">fun</a> <a id="2736" class="Symbol">=</a> <a id="2738" href="Data.Dyck.Rose.html#1185" class="Function">prog→tree</a>
<a id="2748" href="Data.Dyck.Rose.html#2688" class="Function">prog-iso</a> <a id="2757" class="Symbol">.</a><a id="2758" href="Cubical.Foundations.Isomorphism.html#826" class="Field">inv</a> <a id="2762" class="Symbol">=</a> <a id="2764" href="Data.Dyck.Rose.html#1605" class="Function">tree→prog</a>
<a id="2774" href="Data.Dyck.Rose.html#2688" class="Function">prog-iso</a> <a id="2783" class="Symbol">.</a><a id="2784" href="Cubical.Foundations.Isomorphism.html#842" class="Field">rightInv</a> <a id="2793" class="Symbol">=</a> <a id="2795" href="Data.Dyck.Rose.html#2157" class="Function">tree→prog→tree</a>
<a id="2810" href="Data.Dyck.Rose.html#2688" class="Function">prog-iso</a> <a id="2819" class="Symbol">.</a><a id="2820" href="Cubical.Foundations.Isomorphism.html#873" class="Field">leftInv</a>  <a id="2829" class="Symbol">=</a> <a id="2831" href="Data.Dyck.Rose.html#2580" class="Function">prog→tree→prog</a>
</pre></body></html>