<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Control.Comonad.IntervalHeap</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--cubical</a> <a id="23" class="Pragma">--safe</a> <a id="30" class="Pragma">--postfix-projections</a> <a id="52" class="Pragma">--guardedness</a> <a id="66" class="Symbol">#-}</a>

<a id="71" class="Keyword">open</a> <a id="76" class="Keyword">import</a> <a id="83" href="Algebra.html" class="Module">Algebra</a>
<a id="91" class="Keyword">open</a> <a id="96" class="Keyword">import</a> <a id="103" href="Prelude.html" class="Module">Prelude</a>
<a id="111" class="Keyword">open</a> <a id="116" class="Keyword">import</a> <a id="123" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="139" class="Keyword">open</a> <a id="144" class="Keyword">import</a> <a id="151" href="WellFounded.html" class="Module">WellFounded</a>
<a id="163" class="Keyword">open</a> <a id="168" class="Keyword">import</a> <a id="175" href="Algebra.Monus.html" class="Module">Algebra.Monus</a>
<a id="189" class="Keyword">open</a> <a id="194" class="Keyword">import</a> <a id="201" href="Data.Maybe.html" class="Module">Data.Maybe</a>

<a id="213" class="Keyword">module</a> <a id="220" href="Control.Comonad.IntervalHeap.html" class="Module">Control.Comonad.IntervalHeap</a> <a id="249" class="Symbol">{</a><a id="250" href="Control.Comonad.IntervalHeap.html#250" class="Bound">s</a><a id="251" class="Symbol">}</a>
  <a id="255" class="Symbol">(</a><a id="256" href="Control.Comonad.IntervalHeap.html#256" class="Bound">mon</a> <a id="260" class="Symbol">:</a> <a id="262" href="Algebra.Monus.html#201" class="Record">Monus</a> <a id="268" href="Control.Comonad.IntervalHeap.html#250" class="Bound">s</a><a id="269" class="Symbol">)</a>
  <a id="273" class="Symbol">(</a><a id="274" href="Control.Comonad.IntervalHeap.html#274" class="Bound">absorbative</a> <a id="286" class="Symbol">:</a> <a id="288" href="Algebra.Monus.html#1723" class="Function">Monus.Absorbative</a> <a id="306" href="Control.Comonad.IntervalHeap.html#256" class="Bound">mon</a><a id="309" class="Symbol">)</a>
  <a id="313" class="Symbol">(</a><a id="314" href="Control.Comonad.IntervalHeap.html#314" class="Bound">wf</a> <a id="317" class="Symbol">:</a> <a id="319" href="WellFounded.html#385" class="Function">WellFounded</a> <a id="331" class="Symbol">(</a><a id="332" href="Relation.Binary.html#1546" class="Function Operator">Monus._&lt;_</a> <a id="342" href="Control.Comonad.IntervalHeap.html#256" class="Bound">mon</a><a id="345" class="Symbol">))</a> <a id="348" class="Keyword">where</a>

<a id="355" class="Keyword">open</a> <a id="360" href="Algebra.Monus.html#201" class="Module">Monus</a> <a id="366" href="Control.Comonad.IntervalHeap.html#256" class="Bound">mon</a> <a id="370" class="Keyword">public</a> <a id="377" class="Keyword">renaming</a> <a id="386" class="Symbol">(</a>total⇒discrete <a id="402" class="Symbol">to</a> _≟_<a id="408" class="Symbol">)</a>

<a id="411" class="Keyword">record</a> <a id="Heap"></a><a id="418" href="Control.Comonad.IntervalHeap.html#418" class="Record">Heap</a> <a id="423" class="Symbol">{</a><a id="424" href="Control.Comonad.IntervalHeap.html#424" class="Bound">a</a><a id="425" class="Symbol">}</a> <a id="427" class="Symbol">(</a><a id="428" href="Control.Comonad.IntervalHeap.html#428" class="Bound">A</a> <a id="430" class="Symbol">:</a> <a id="432" href="Level.html#192" class="Function">Type</a> <a id="437" href="Control.Comonad.IntervalHeap.html#424" class="Bound">a</a><a id="438" class="Symbol">)</a> <a id="440" class="Symbol">:</a> <a id="442" href="Level.html#192" class="Function">Type</a> <a id="447" class="Symbol">(</a><a id="448" href="Control.Comonad.IntervalHeap.html#250" class="Bound">s</a> <a id="450" href="Agda.Primitive.html#636" class="Primitive Operator">ℓ⊔</a> <a id="453" href="Control.Comonad.IntervalHeap.html#424" class="Bound">a</a><a id="454" class="Symbol">)</a> <a id="456" class="Keyword">where</a>
  <a id="464" class="Keyword">coinductive</a><a id="475" class="Symbol">;</a> <a id="477" class="Keyword">constructor</a> <a id="_≺_"></a><a id="489" href="Control.Comonad.IntervalHeap.html#489" class="CoinductiveConstructor Operator">_≺_</a>
  <a id="495" class="Keyword">field</a>
    <a id="Heap.v"></a><a id="505" href="Control.Comonad.IntervalHeap.html#505" class="Field">v</a> <a id="507" class="Symbol">:</a> <a id="509" href="Control.Comonad.IntervalHeap.html#428" class="Bound">A</a>
    <a id="Heap.next"></a><a id="515" href="Control.Comonad.IntervalHeap.html#515" class="Field">next</a> <a id="520" class="Symbol">:</a> <a id="522" href="Data.Maybe.Base.html#91" class="Datatype">Maybe</a> <a id="528" class="Symbol">(</a><a id="529" href="Data.Sigma.Base.html#248" class="Function">∃[</a> <a id="532" href="Control.Comonad.IntervalHeap.html#532" class="Bound">s</a> <a id="534" href="Data.Sigma.Base.html#248" class="Function">]</a> <a id="536" class="Symbol">((</a><a id="538" href="Control.Comonad.IntervalHeap.html#532" class="Bound">s</a> <a id="540" href="Path.html#506" class="Function Operator">≢</a> <a id="542" href="Algebra.html#1165" class="Function">ε</a><a id="543" class="Symbol">)</a> <a id="545" href="Data.Sigma.Base.html#519" class="Function Operator">×</a> <a id="547" href="Control.Comonad.IntervalHeap.html#418" class="Record">Heap</a> <a id="552" href="Control.Comonad.IntervalHeap.html#428" class="Bound">A</a><a id="553" class="Symbol">))</a>
<a id="556" class="Keyword">open</a> <a id="561" href="Control.Comonad.IntervalHeap.html#418" class="Module">Heap</a> <a id="566" class="Keyword">public</a>


<a id="State"></a><a id="575" href="Control.Comonad.IntervalHeap.html#575" class="Function">State</a> <a id="581" class="Symbol">:</a> <a id="583" href="Level.html#192" class="Function">Type</a> <a id="588" href="Level.html#372" class="Generalizable">a</a> <a id="590" class="Symbol">→</a> <a id="592" href="Level.html#192" class="Function">Type</a> <a id="597" class="Symbol">_</a>
<a id="599" href="Control.Comonad.IntervalHeap.html#575" class="Function">State</a> <a id="605" href="Control.Comonad.IntervalHeap.html#605" class="Bound">A</a> <a id="607" class="Symbol">=</a> <a id="609" href="Algebra.html#1122" class="Function">𝑆</a> <a id="611" class="Symbol">→</a> <a id="613" href="Control.Comonad.IntervalHeap.html#605" class="Bound">A</a> <a id="615" href="Data.Sigma.Base.html#519" class="Function Operator">×</a> <a id="617" href="Algebra.html#1122" class="Function">𝑆</a>

<a id="pop′"></a><a id="620" href="Control.Comonad.IntervalHeap.html#620" class="Function">pop′</a> <a id="625" class="Symbol">:</a> <a id="627" class="Symbol">(</a><a id="628" href="Control.Comonad.IntervalHeap.html#628" class="Bound">s</a> <a id="630" class="Symbol">:</a> <a id="632" href="Algebra.html#1122" class="Function">𝑆</a><a id="633" class="Symbol">)</a> <a id="635" class="Symbol">→</a> <a id="637" href="WellFounded.html#87" class="Datatype">Acc</a> <a id="641" href="Relation.Binary.html#1546" class="Function Operator">_&lt;_</a> <a id="645" href="Control.Comonad.IntervalHeap.html#628" class="Bound">s</a> <a id="647" class="Symbol">→</a> <a id="649" href="Control.Comonad.IntervalHeap.html#418" class="Record">Heap</a> <a id="654" href="Level.html#388" class="Generalizable">A</a> <a id="656" class="Symbol">→</a> <a id="658" href="Level.html#388" class="Generalizable">A</a> <a id="660" href="Data.Sigma.Base.html#519" class="Function Operator">×</a> <a id="662" href="Algebra.html#1122" class="Function">𝑆</a>
<a id="664" href="Control.Comonad.IntervalHeap.html#620" class="Function">pop′</a> <a id="669" href="Control.Comonad.IntervalHeap.html#669" class="Bound">s₂</a> <a id="672" href="Control.Comonad.IntervalHeap.html#672" class="Bound">a</a> <a id="674" href="Control.Comonad.IntervalHeap.html#674" class="Bound">xs</a> <a id="677" class="Keyword">with</a> <a id="682" href="Control.Comonad.IntervalHeap.html#674" class="Bound">xs</a> <a id="685" class="Symbol">.</a><a id="686" href="Control.Comonad.IntervalHeap.html#515" class="Field">next</a>
<a id="691" href="Control.Comonad.IntervalHeap.html#620" class="Function">pop′</a> <a id="696" href="Control.Comonad.IntervalHeap.html#696" class="Bound">s₂</a> <a id="699" href="Control.Comonad.IntervalHeap.html#699" class="Bound">a</a> <a id="701" href="Control.Comonad.IntervalHeap.html#701" class="Bound">xs</a> <a id="704" class="Symbol">|</a> <a id="706" href="Data.Maybe.Base.html#127" class="InductiveConstructor">nothing</a> <a id="714" class="Symbol">=</a> <a id="716" href="Control.Comonad.IntervalHeap.html#701" class="Bound">xs</a> <a id="719" class="Symbol">.</a><a id="720" href="Control.Comonad.IntervalHeap.html#505" class="Field">v</a> <a id="722" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="724" href="Control.Comonad.IntervalHeap.html#696" class="Bound">s₂</a>
<a id="727" href="Control.Comonad.IntervalHeap.html#620" class="Function">pop′</a> <a id="732" href="Control.Comonad.IntervalHeap.html#732" class="Bound">s₂</a> <a id="735" href="Control.Comonad.IntervalHeap.html#735" class="Bound">a</a> <a id="737" href="Control.Comonad.IntervalHeap.html#737" class="Bound">xs</a> <a id="740" class="Symbol">|</a> <a id="742" href="Data.Maybe.Base.html#148" class="InductiveConstructor">just</a> <a id="747" class="Symbol">(</a><a id="748" href="Control.Comonad.IntervalHeap.html#748" class="Bound">s₁</a> <a id="751" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="753" href="Control.Comonad.IntervalHeap.html#753" class="Bound">s₁≢ε</a> <a id="758" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="760" href="Control.Comonad.IntervalHeap.html#760" class="Bound">ys</a><a id="762" class="Symbol">)</a> <a id="764" class="Keyword">with</a> <a id="769" href="Control.Comonad.IntervalHeap.html#748" class="Bound">s₁</a> <a id="772" href="Relation.Binary.html#3866" class="Function Operator">≤?</a> <a id="775" href="Control.Comonad.IntervalHeap.html#732" class="Bound">s₂</a>
<a id="778" href="Control.Comonad.IntervalHeap.html#620" class="Function">pop′</a> <a id="783" href="Control.Comonad.IntervalHeap.html#783" class="Bound">s₂</a> <a id="786" href="Control.Comonad.IntervalHeap.html#786" class="Bound">a</a> <a id="788" href="Control.Comonad.IntervalHeap.html#788" class="Bound">xs</a> <a id="791" class="Symbol">|</a> <a id="793" href="Data.Maybe.Base.html#148" class="InductiveConstructor">just</a> <a id="798" class="Symbol">(</a><a id="799" href="Control.Comonad.IntervalHeap.html#799" class="Bound">s₁</a> <a id="802" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="804" href="Control.Comonad.IntervalHeap.html#804" class="Bound">s₁≢ε</a> <a id="809" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="811" href="Control.Comonad.IntervalHeap.html#811" class="Bound">ys</a><a id="813" class="Symbol">)</a> <a id="815" class="Symbol">|</a> <a id="817" href="Relation.Nullary.Decidable.Base.html#425" class="InductiveConstructor">no</a> <a id="820" href="Control.Comonad.IntervalHeap.html#820" class="Bound">s₁≰s₂</a> <a id="826" class="Symbol">=</a> <a id="828" href="Control.Comonad.IntervalHeap.html#788" class="Bound">xs</a> <a id="831" class="Symbol">.</a><a id="832" href="Control.Comonad.IntervalHeap.html#505" class="Field">v</a> <a id="834" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="836" href="Agda.Builtin.Sigma.html#252" class="Field">fst</a> <a id="840" class="Symbol">(</a><a id="841" href="Relation.Binary.html#3562" class="Function">&lt;⇒≤</a> <a id="845" href="Control.Comonad.IntervalHeap.html#820" class="Bound">s₁≰s₂</a><a id="850" class="Symbol">)</a>
<a id="852" href="Control.Comonad.IntervalHeap.html#620" class="Function">pop′</a> <a id="857" href="Control.Comonad.IntervalHeap.html#857" class="Bound">s₂</a> <a id="860" class="Symbol">(</a><a id="861" href="WellFounded.html#163" class="InductiveConstructor">acc</a> <a id="865" href="Control.Comonad.IntervalHeap.html#865" class="Bound">wf</a><a id="867" class="Symbol">)</a> <a id="869" href="Control.Comonad.IntervalHeap.html#869" class="Bound">xs</a> <a id="872" class="Symbol">|</a> <a id="874" href="Data.Maybe.Base.html#148" class="InductiveConstructor">just</a> <a id="879" class="Symbol">(</a><a id="880" href="Control.Comonad.IntervalHeap.html#880" class="Bound">s₁</a> <a id="883" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="885" href="Control.Comonad.IntervalHeap.html#885" class="Bound">s₁≢ε</a> <a id="890" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="892" href="Control.Comonad.IntervalHeap.html#892" class="Bound">ys</a><a id="894" class="Symbol">)</a> <a id="896" class="Symbol">|</a> <a id="898" href="Relation.Nullary.Decidable.Base.html#391" class="InductiveConstructor">yes</a> <a id="902" class="Symbol">(</a><a id="903" href="Control.Comonad.IntervalHeap.html#903" class="Bound">k₁</a> <a id="906" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="908" href="Control.Comonad.IntervalHeap.html#908" class="Bound">s₂≡s₁∙k₁</a><a id="916" class="Symbol">)</a> <a id="918" class="Symbol">=</a> <a id="920" href="Control.Comonad.IntervalHeap.html#620" class="Function">pop′</a> <a id="925" href="Control.Comonad.IntervalHeap.html#903" class="Bound">k₁</a> <a id="928" class="Symbol">(</a><a id="929" href="Control.Comonad.IntervalHeap.html#865" class="Bound">wf</a> <a id="932" href="Control.Comonad.IntervalHeap.html#903" class="Bound">k₁</a> <a id="935" href="Control.Comonad.IntervalHeap.html#955" class="Function">lemma</a><a id="940" class="Symbol">)</a> <a id="942" href="Control.Comonad.IntervalHeap.html#892" class="Bound">ys</a>
  <a id="947" class="Keyword">where</a>
  <a id="955" href="Control.Comonad.IntervalHeap.html#955" class="Function">lemma</a> <a id="961" class="Symbol">:</a> <a id="963" href="Control.Comonad.IntervalHeap.html#903" class="Bound">k₁</a> <a id="966" href="Relation.Binary.html#1546" class="Function Operator">&lt;</a> <a id="968" href="Control.Comonad.IntervalHeap.html#857" class="Bound">s₂</a>
  <a id="973" href="Control.Comonad.IntervalHeap.html#955" class="Function">lemma</a> <a id="979" class="Symbol">(</a><a id="980" href="Control.Comonad.IntervalHeap.html#980" class="Bound">k₂</a> <a id="983" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="985" href="Control.Comonad.IntervalHeap.html#985" class="Bound">k₁≡s₂∙k₂</a><a id="993" class="Symbol">)</a> <a id="995" class="Symbol">=</a> <a id="997" href="Control.Comonad.IntervalHeap.html#885" class="Bound">s₁≢ε</a> <a id="1002" class="Symbol">(</a><a id="1003" href="Algebra.Monus.html#1066" class="Function">zeroSumFree</a> <a id="1015" href="Control.Comonad.IntervalHeap.html#880" class="Bound">s₁</a> <a id="1018" href="Control.Comonad.IntervalHeap.html#980" class="Bound">k₂</a> <a id="1021" class="Symbol">(</a><a id="1022" href="Control.Comonad.IntervalHeap.html#274" class="Bound">absorbative</a> <a id="1034" class="Symbol">_</a> <a id="1036" class="Symbol">_</a> <a id="1038" href="Control.Comonad.IntervalHeap.html#1056" class="Function">p</a><a id="1039" class="Symbol">))</a>
    <a id="1046" class="Keyword">where</a>
    <a id="1056" href="Control.Comonad.IntervalHeap.html#1056" class="Function">p</a> <a id="1058" class="Symbol">:</a> <a id="1060" href="Control.Comonad.IntervalHeap.html#903" class="Bound">k₁</a> <a id="1063" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1065" href="Control.Comonad.IntervalHeap.html#903" class="Bound">k₁</a> <a id="1068" href="Algebra.html#1142" class="Function Operator">∙</a> <a id="1070" class="Symbol">(</a><a id="1071" href="Control.Comonad.IntervalHeap.html#880" class="Bound">s₁</a> <a id="1074" href="Algebra.html#1142" class="Function Operator">∙</a> <a id="1076" href="Control.Comonad.IntervalHeap.html#980" class="Bound">k₂</a><a id="1078" class="Symbol">)</a>
    <a id="1084" href="Control.Comonad.IntervalHeap.html#1056" class="Function">p</a> <a id="1086" class="Symbol">=</a> <a id="1088" href="Control.Comonad.IntervalHeap.html#985" class="Bound">k₁≡s₂∙k₂</a> <a id="1097" href="Cubical.Foundations.Id.html#747" class="Function Operator">;</a> <a id="1099" href="Cubical.Foundations.Id.html#782" class="Function">cong</a> <a id="1104" class="Symbol">(</a><a id="1105" href="Algebra.html#1142" class="Function Operator">_∙</a> <a id="1108" href="Control.Comonad.IntervalHeap.html#980" class="Bound">k₂</a><a id="1110" class="Symbol">)</a> <a id="1112" href="Control.Comonad.IntervalHeap.html#908" class="Bound">s₂≡s₁∙k₁</a> <a id="1121" href="Cubical.Foundations.Id.html#747" class="Function Operator">;</a> <a id="1123" href="Cubical.Foundations.Id.html#782" class="Function">cong</a> <a id="1128" class="Symbol">(</a><a id="1129" href="Algebra.html#1142" class="Function Operator">_∙</a> <a id="1132" href="Control.Comonad.IntervalHeap.html#980" class="Bound">k₂</a><a id="1134" class="Symbol">)</a> <a id="1136" class="Symbol">(</a><a id="1137" href="Algebra.html#2015" class="Function">comm</a> <a id="1142" href="Control.Comonad.IntervalHeap.html#880" class="Bound">s₁</a> <a id="1145" href="Control.Comonad.IntervalHeap.html#903" class="Bound">k₁</a><a id="1147" class="Symbol">)</a> <a id="1149" href="Cubical.Foundations.Id.html#747" class="Function Operator">;</a> <a id="1151" href="Algebra.html#1180" class="Function">assoc</a> <a id="1157" href="Control.Comonad.IntervalHeap.html#903" class="Bound">k₁</a> <a id="1160" href="Control.Comonad.IntervalHeap.html#880" class="Bound">s₁</a> <a id="1163" href="Control.Comonad.IntervalHeap.html#980" class="Bound">k₂</a>

<a id="pop"></a><a id="1167" href="Control.Comonad.IntervalHeap.html#1167" class="Function">pop</a> <a id="1171" class="Symbol">:</a> <a id="1173" href="Control.Comonad.IntervalHeap.html#418" class="Record">Heap</a> <a id="1178" href="Level.html#388" class="Generalizable">A</a> <a id="1180" class="Symbol">→</a> <a id="1182" href="Control.Comonad.IntervalHeap.html#575" class="Function">State</a> <a id="1188" href="Level.html#388" class="Generalizable">A</a>
<a id="1190" href="Control.Comonad.IntervalHeap.html#1167" class="Function">pop</a> <a id="1194" href="Control.Comonad.IntervalHeap.html#1194" class="Bound">xs</a> <a id="1197" href="Control.Comonad.IntervalHeap.html#1197" class="Bound">s</a> <a id="1199" class="Symbol">=</a> <a id="1201" href="Control.Comonad.IntervalHeap.html#620" class="Function">pop′</a> <a id="1206" href="Control.Comonad.IntervalHeap.html#1197" class="Bound">s</a> <a id="1208" class="Symbol">(</a><a id="1209" href="Control.Comonad.IntervalHeap.html#314" class="Bound">wf</a> <a id="1212" href="Control.Comonad.IntervalHeap.html#1197" class="Bound">s</a><a id="1213" class="Symbol">)</a> <a id="1215" href="Control.Comonad.IntervalHeap.html#1194" class="Bound">xs</a>

<a id="1219" class="Keyword">mutual</a>
  <a id="stepFrom"></a><a id="1228" href="Control.Comonad.IntervalHeap.html#1228" class="Function">stepFrom</a> <a id="1237" class="Symbol">:</a> <a id="1239" href="Control.Comonad.IntervalHeap.html#575" class="Function">State</a> <a id="1245" href="Level.html#388" class="Generalizable">A</a> <a id="1247" class="Symbol">→</a> <a id="1249" class="Symbol">(</a><a id="1250" href="Control.Comonad.IntervalHeap.html#1250" class="Bound">s</a> <a id="1252" class="Symbol">:</a> <a id="1254" href="Algebra.html#1122" class="Function">𝑆</a><a id="1255" class="Symbol">)</a> <a id="1257" class="Symbol">→</a> <a id="1259" href="Relation.Nullary.Decidable.Base.html#253" class="Record">Dec</a> <a id="1263" class="Symbol">(</a><a id="1264" href="Control.Comonad.IntervalHeap.html#1250" class="Bound">s</a> <a id="1266" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1268" href="Algebra.html#1165" class="Function">ε</a><a id="1269" class="Symbol">)</a> <a id="1271" class="Symbol">→</a> <a id="1273" href="Data.Maybe.Base.html#91" class="Datatype">Maybe</a> <a id="1279" class="Symbol">(</a><a id="1280" href="Data.Sigma.Base.html#248" class="Function">∃[</a> <a id="1283" href="Control.Comonad.IntervalHeap.html#1283" class="Bound">s</a> <a id="1285" href="Data.Sigma.Base.html#248" class="Function">]</a> <a id="1287" class="Symbol">((</a><a id="1289" href="Control.Comonad.IntervalHeap.html#1283" class="Bound">s</a> <a id="1291" href="Path.html#506" class="Function Operator">≢</a> <a id="1293" href="Algebra.html#1165" class="Function">ε</a><a id="1294" class="Symbol">)</a> <a id="1296" href="Data.Sigma.Base.html#519" class="Function Operator">×</a> <a id="1298" href="Control.Comonad.IntervalHeap.html#418" class="Record">Heap</a> <a id="1303" href="Level.html#388" class="Generalizable">A</a><a id="1304" class="Symbol">))</a>
  <a id="1309" href="Control.Comonad.IntervalHeap.html#1228" class="Function">stepFrom</a> <a id="1318" href="Control.Comonad.IntervalHeap.html#1318" class="Bound">f</a> <a id="1320" href="Control.Comonad.IntervalHeap.html#1320" class="Bound">s</a> <a id="1322" class="Symbol">(</a><a id="1323" href="Relation.Nullary.Decidable.Base.html#391" class="InductiveConstructor">yes</a> <a id="1327" href="Control.Comonad.IntervalHeap.html#1327" class="Bound">p</a><a id="1328" class="Symbol">)</a> <a id="1330" class="Symbol">=</a> <a id="1332" href="Data.Maybe.Base.html#127" class="InductiveConstructor">nothing</a>
  <a id="1342" href="Control.Comonad.IntervalHeap.html#1228" class="Function">stepFrom</a> <a id="1351" href="Control.Comonad.IntervalHeap.html#1351" class="Bound">f</a> <a id="1353" href="Control.Comonad.IntervalHeap.html#1353" class="Bound">s</a> <a id="1355" class="Symbol">(</a><a id="1356" href="Relation.Nullary.Decidable.Base.html#425" class="InductiveConstructor">no</a> <a id="1359" href="Control.Comonad.IntervalHeap.html#1359" class="Bound">¬p</a><a id="1361" class="Symbol">)</a> <a id="1363" class="Symbol">=</a> <a id="1365" href="Data.Maybe.Base.html#148" class="InductiveConstructor">just</a> <a id="1370" class="Symbol">(</a><a id="1371" href="Control.Comonad.IntervalHeap.html#1353" class="Bound">s</a> <a id="1373" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1375" href="Control.Comonad.IntervalHeap.html#1359" class="Bound">¬p</a> <a id="1378" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1380" href="Control.Comonad.IntervalHeap.html#1406" class="Function">tabulate</a> <a id="1389" class="Symbol">(</a><a id="1390" href="Control.Comonad.IntervalHeap.html#1351" class="Bound">f</a> <a id="1392" href="Function.html#273" class="Function Operator">∘′</a> <a id="1395" href="Algebra.html#1142" class="Function Operator">_∙_</a> <a id="1399" href="Control.Comonad.IntervalHeap.html#1353" class="Bound">s</a><a id="1400" class="Symbol">))</a>

  <a id="tabulate"></a><a id="1406" href="Control.Comonad.IntervalHeap.html#1406" class="Function">tabulate</a> <a id="1415" class="Symbol">:</a> <a id="1417" href="Control.Comonad.IntervalHeap.html#575" class="Function">State</a> <a id="1423" href="Level.html#388" class="Generalizable">A</a> <a id="1425" class="Symbol">→</a> <a id="1427" href="Control.Comonad.IntervalHeap.html#418" class="Record">Heap</a> <a id="1432" href="Level.html#388" class="Generalizable">A</a>
  <a id="1436" href="Control.Comonad.IntervalHeap.html#1406" class="Function">tabulate</a> <a id="1445" href="Control.Comonad.IntervalHeap.html#1445" class="Bound">f</a> <a id="1447" class="Symbol">=</a> <a id="1449" class="Keyword">let</a> <a id="1453" href="Control.Comonad.IntervalHeap.html#1453" class="Bound">x</a> <a id="1455" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1457" href="Control.Comonad.IntervalHeap.html#1457" class="Bound">s</a> <a id="1459" class="Symbol">=</a> <a id="1461" href="Control.Comonad.IntervalHeap.html#1445" class="Bound">f</a> <a id="1463" href="Algebra.html#1165" class="Function">ε</a> <a id="1465" class="Keyword">in</a> <a id="1468" class="Symbol">λ</a> <a id="1470" class="Keyword">where</a>
    <a id="1480" class="Symbol">.</a><a id="1481" href="Control.Comonad.IntervalHeap.html#505" class="Field">v</a> <a id="1483" class="Symbol">→</a> <a id="1485" href="Control.Comonad.IntervalHeap.html#1453" class="Bound">x</a>
    <a id="1491" class="Symbol">.</a><a id="1492" href="Control.Comonad.IntervalHeap.html#515" class="Field">next</a> <a id="1497" class="Symbol">→</a> <a id="1499" href="Control.Comonad.IntervalHeap.html#1228" class="Function">stepFrom</a> <a id="1508" href="Control.Comonad.IntervalHeap.html#1445" class="Bound">f</a> <a id="1510" href="Control.Comonad.IntervalHeap.html#1457" class="Bound">s</a> <a id="1512" class="Symbol">(</a><a id="1513" href="Control.Comonad.IntervalHeap.html#1457" class="Bound">s</a> <a id="1515" href="Control.Comonad.IntervalHeap.html#405" class="Function Operator">≟</a> <a id="1517" href="Algebra.html#1165" class="Function">ε</a><a id="1518" class="Symbol">)</a>
</pre></body></html>