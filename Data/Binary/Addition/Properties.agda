{-# OPTIONS --cubical --safe #-}

module Data.Binary.Addition.Properties where

open import Prelude
open import Data.Binary.Definition
open import Data.Binary.Addition
open import Data.Binary.Conversion
import Data.Nat as РёЋ
import Data.Nat.Properties as РёЋ
open import Path.Reasoning
open import Data.Binary.Isomorphism


+-cong : Рѕђ xs ys Рєњ РЪд xs + ys РЄЊРЪД РЅА РЪд xs РЄЊРЪД РёЋ.+ РЪд ys РЄЊРЪД
addРѓЂ-cong : Рѕђ xs ys Рєњ РЪд addРѓЂ xs ys РЄЊРЪД РЅА 1 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.+ РЪд ys РЄЊРЪД
addРѓѓ-cong : Рѕђ xs ys Рєњ РЪд addРѓѓ xs ys РЄЊРЪД РЅА 2 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.+ РЪд ys РЄЊРЪД

lemmaРѓЂ : Рѕђ xs ys Рєњ РЪд addРѓЂ xs ys РЄЊРЪД РёЋ.* 2 РЅА РЪд xs РЄЊРЪД РёЋ.* 2 РёЋ.+ (2 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2)
lemmaРѓЂ xs ys =
  РЪд addРѓЂ xs ys РЄЊРЪД РёЋ.* 2 РЅАРЪе cong (РёЋ._* 2) (addРѓЂ-cong xs ys) РЪЕ
  2 РёЋ.+ (РЪд xs РЄЊРЪД РёЋ.+ РЪд ys РЄЊРЪД) РёЋ.* 2 РЅАРЪе cong (2 РёЋ.+_ ) (РёЋ.+-*-distrib РЪд xs РЄЊРЪД РЪд ys РЄЊРЪД 2) РЪЕ
  2 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.* 2 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2 РЅАРЪе cong (РёЋ._+ (РЪд ys РЄЊРЪД РёЋ.* 2)) (РёЋ.+-comm 2 (РЪд xs РЄЊРЪД РёЋ.* 2)) РЪЕ
  РЪд xs РЄЊРЪД РёЋ.* 2 РёЋ.+ 2 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2 РЅАРЪе РёЋ.+-assoc (РЪд xs РЄЊРЪД РёЋ.* 2) 2 (РЪд ys РЄЊРЪД РёЋ.* 2) РЪЕ
  РЪд xs РЄЊРЪД РёЋ.* 2 РёЋ.+ (2 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2) Рѕј

lemmaРѓѓ : Рѕђ xs ys Рєњ РЪд addРѓЂ xs ys РЄЊРЪД РёЋ.* 2 РЅА (1 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.* 2) РёЋ.+ (1 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2)
lemmaРѓѓ xs ys =
  РЪд addРѓЂ xs ys РЄЊРЪД РёЋ.* 2 РЅАРЪе cong (РёЋ._* 2) (addРѓЂ-cong xs ys) РЪЕ
  (1 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.+ РЪд ys РЄЊРЪД) РёЋ.* 2 РЅАРЪе РёЋ.+-*-distrib (1 РёЋ.+ РЪд xs РЄЊРЪД) РЪд ys РЄЊРЪД 2 РЪЕ
  2 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.* 2 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2 РЅА╦ўРЪе РёЋ.+-suc (1 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.* 2) (РЪд ys РЄЊРЪД РёЋ.* 2) РЪЕ
  (1 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.* 2) РёЋ.+ (1 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2) Рѕј

lemmaРѓЃ : Рѕђ xs ys Рєњ РЪд addРѓѓ xs ys РЄЊРЪД РёЋ.* 2 РЅА suc (suc (РЪд xs РЄЊРЪД РёЋ.* 2 РёЋ.+ suc (suc (РЪд ys РЄЊРЪД РёЋ.* 2))))
lemmaРѓЃ xs ys =
  РЪд addРѓѓ xs ys РЄЊРЪД РёЋ.* 2 РЅАРЪе cong (РёЋ._* 2) (addРѓѓ-cong xs ys) РЪЕ
  (2 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.+ РЪд ys РЄЊРЪД) РёЋ.* 2 РЅА╦ўРЪе cong (РёЋ._* 2) (РёЋ.+-suc (1 РёЋ.+ РЪд xs РЄЊРЪД) РЪд ys РЄЊРЪД) РЪЕ
  ((1 РёЋ.+ РЪд xs РЄЊРЪД) РёЋ.+ (1 РёЋ.+ РЪд ys РЄЊРЪД)) РёЋ.* 2 РЅАРЪе РёЋ.+-*-distrib (1 РёЋ.+ РЪд xs РЄЊРЪД) (1 РёЋ.+ РЪд ys РЄЊРЪД) 2 РЪЕ
  suc (suc (РЪд xs РЄЊРЪД РёЋ.* 2 РёЋ.+ suc (suc (РЪд ys РЄЊРЪД РёЋ.* 2)))) Рѕј

addРѓЂ-cong 0рхЄ ys = inc-suc ys
addРѓЂ-cong (1рхЄ xs) 0рхЄ = cong (2 РёЋ.+_) (sym (РёЋ.+-id╩│ (РЪд xs РЄЊРЪД РёЋ.* 2)))
addРѓЂ-cong (2рхЄ xs) 0рхЄ = cong suc (cong (РёЋ._* 2) (inc-suc xs) ═Й cong (2 РёЋ.+_) (sym (РёЋ.+-id╩│ (РЪд xs РЄЊРЪД РёЋ.* 2))))
addРѓЂ-cong (1рхЄ xs) (1рхЄ ys) = cong suc (lemmaРѓѓ xs ys)
addРѓЂ-cong (1рхЄ xs) (2рхЄ ys) = cong (2 РёЋ.+_) (lemmaРѓЂ xs ys)
addРѓЂ-cong (2рхЄ xs) (1рхЄ ys) = cong (2 РёЋ.+_) (lemmaРѓѓ xs ys)
addРѓЂ-cong (2рхЄ xs) (2рхЄ ys) = cong (1 РёЋ.+_) (lemmaРѓЃ xs ys)

addРѓѓ-cong 0рхЄ 0рхЄ = refl
addРѓѓ-cong 0рхЄ (1рхЄ ys) = cong (1 РёЋ.+_) (cong (РёЋ._* 2) (inc-suc ys))
addРѓѓ-cong 0рхЄ (2рхЄ ys) = cong (2 РёЋ.+_) (cong (РёЋ._* 2) (inc-suc ys))
addРѓѓ-cong (1рхЄ xs) 0рхЄ = cong (1 РёЋ.+_) ((cong (РёЋ._* 2) (inc-suc xs)) ═Й cong (2 РёЋ.+_) (sym (РёЋ.+-id╩│ _)))
addРѓѓ-cong (2рхЄ xs) 0рхЄ = cong (2 РёЋ.+_) ((cong (РёЋ._* 2) (inc-suc xs)) ═Й cong (2 РёЋ.+_) (sym (РёЋ.+-id╩│ _)))
addРѓѓ-cong (1рхЄ xs) (1рхЄ ys) = cong (2 РёЋ.+_ ) (lemmaРѓѓ xs ys)
addРѓѓ-cong (1рхЄ xs) (2рхЄ ys) = cong (1 РёЋ.+_) (lemmaРѓЃ xs ys)
addРѓѓ-cong (2рхЄ xs) (1рхЄ ys) = cong (1 РёЋ.+_) (lemmaРѓЃ xs ys ═Й РёЋ.+-suc (2 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.* 2) (1 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2))
addРѓѓ-cong (2рхЄ xs) (2рхЄ ys) = cong (2 РёЋ.+_) (lemmaРѓЃ xs ys)

+-cong 0рхЄ ys = refl
+-cong (1рхЄ xs) 0рхЄ = cong suc (sym (РёЋ.+-id╩│ (РЪд xs РЄЊРЪД РёЋ.* 2)))
+-cong (2рхЄ xs) 0рхЄ = cong (suc Рѕў suc) (sym (РёЋ.+-id╩│ (РЪд xs РЄЊРЪД РёЋ.* 2)))
+-cong (1рхЄ xs) (1рхЄ ys) =
  2 РёЋ.+ РЪд xs + ys РЄЊРЪД РёЋ.* 2 РЅАРЪе cong (╬╗ xy Рєњ 2 РёЋ.+ xy РёЋ.* 2) (+-cong xs ys) РЪЕ
  2 РёЋ.+ (РЪд xs РЄЊРЪД РёЋ.+ РЪд ys РЄЊРЪД) РёЋ.* 2 РЅАРЪе cong (2 РёЋ.+_) (РёЋ.+-*-distrib РЪд xs РЄЊРЪД РЪд ys РЄЊРЪД 2) РЪЕ
  2 РёЋ.+ (РЪд xs РЄЊРЪД РёЋ.* 2 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2) РЅА╦ўРЪе cong suc (РёЋ.+-suc (РЪд xs РЄЊРЪД РёЋ.* 2) (РЪд ys РЄЊРЪД РёЋ.* 2)) РЪЕ
  1 РёЋ.+ РЪд xs РЄЊРЪД РёЋ.* 2 РёЋ.+ (1 РёЋ.+ РЪд ys РЄЊРЪД РёЋ.* 2) Рѕј
+-cong (1рхЄ xs) (2рхЄ ys) = cong suc (lemmaРѓЂ xs ys)
+-cong (2рхЄ xs) (1рхЄ ys) = cong suc (lemmaРѓѓ xs ys)
+-cong (2рхЄ xs) (2рхЄ ys) = cong (2 РёЋ.+_) (lemmaРѓЂ xs ys)

+-cong╦ў : Рѕђ xs ys Рєњ РЪд xs РёЋ.+ ys РЄЉРЪД РЅА РЪд xs РЄЉРЪД + РЪд ys РЄЉРЪД
+-cong╦ў xs ys =
  РЪд xs РёЋ.+ ys РЄЉРЪД РЅА╦ўРЪе cong РЪд_РЄЉРЪД (congРѓѓ РёЋ._+_ (­Юћ╣-rightInv xs) (­Юћ╣-rightInv ys)) РЪЕ
  РЪд РЪд РЪд xs РЄЉРЪД РЄЊРЪД РёЋ.+ РЪд РЪд ys РЄЉРЪД РЄЊРЪД РЄЉРЪД РЅА╦ўРЪе cong РЪд_РЄЉРЪД (+-cong РЪд xs РЄЉРЪД РЪд ys РЄЉРЪД) РЪЕ
  РЪд РЪд РЪд xs РЄЉРЪД + РЪд ys РЄЉРЪД РЄЊРЪД РЄЉРЪД РЅАРЪе ­Юћ╣-leftInv (РЪд xs РЄЉРЪД + РЪд ys РЄЉРЪД) РЪЕ
  РЪд xs РЄЉРЪД + РЪд ys РЄЉРЪД Рѕј
