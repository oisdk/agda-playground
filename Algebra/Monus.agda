{-# OPTIONS --safe #-}

-- This is a file for dealing with Monuses: these are monoids that are like the
-- positive half of a group. Much of my info on them comes from these papers:
--
-- * Wehrung, Friedrich. â€˜Injective Positively Ordered Monoids Iâ€™. Journal of
--   Pure and Applied Algebra 83, no. 1 (11 November 1992): 43â€“82.
--   https://doi.org/10.1016/0022-4049(92)90104-N.
-- * Wehrung, Friedrich. â€˜Embedding Simple Commutative Monoids into Simple
--   Refinement Monoidsâ€™. Semigroup Forum 56, no. 1 (January 1998): 104â€“29.
--   https://doi.org/10.1007/s00233-002-7008-0.
-- * Amer, K. â€˜Equationally Complete Classes of Commutative Monoids with Monusâ€™.
--   Algebra Universalis 18, no. 1 (1 February 1984): 129â€“31.
--   https://doi.org/10.1007/BF01182254.
-- * Wehrung, Friedrich. â€˜Metric Properties of Positively Ordered Monoidsâ€™.
--   Forum Mathematicum 5, no. 5 (1993).
--   https://doi.org/10.1515/form.1993.5.183.
-- * Wehrung, Friedrich. â€˜Restricted Injectivity, Transfer Property and
--   Decompositions of Separative Positively Ordered Monoids.â€™ Communications in
--   Algebra 22, no. 5 (1 January 1994): 1747â€“81.
--   https://doi.org/10.1080/00927879408824934.
--
-- These monoids have a preorder defined on them, the algebraic preorder:
-- 
--   x â‰¤ y = âˆƒ[ z ] (y â‰¡ x âˆ™ z)
--
-- The _âˆ¸_ operator extracts the z from above, if it exists.

module Algebra.Monus where

open import Prelude
open import Algebra
open import Relation.Binary
open import Path.Reasoning
open import Function.Reasoning

-- Positively ordered monoids.
--
-- These are monoids which have a preorder that respects the monoid operation
-- in a straightforward way.
record POM â„“â‚ â„“â‚‚ : Type (â„“suc (â„“â‚ â„“âŠ” â„“â‚‚)) where
  field commutativeMonoid : CommutativeMonoid â„“â‚
  open CommutativeMonoid commutativeMonoid public
  field preorder : Preorder ğ‘† â„“â‚‚
  open Preorder preorder public renaming (refl to â‰¤-refl)
  field
    positive : âˆ€ x â†’ Îµ â‰¤ x
    â‰¤-cong : âˆ€ x {y z} â†’ y â‰¤ z â†’ x âˆ™ y â‰¤ x âˆ™ z

  xâ‰¤xâˆ™y : âˆ€ {x y} â†’ x â‰¤ x âˆ™ y
  xâ‰¤xâˆ™y {x} {y} = subst (_â‰¤ x âˆ™ y) (âˆ™Îµ x) (â‰¤-cong x (positive y))

  â‰¤-congÊ³ : âˆ€ x {y z} â†’ y â‰¤ z â†’ y âˆ™ x â‰¤ z âˆ™ x
  â‰¤-congÊ³ x {y} {z} p = substâ‚‚ _â‰¤_ (comm x y) (comm x z) (â‰¤-cong x p)

  alg-â‰¤-trans : âˆ€ {x y z kâ‚ kâ‚‚} â†’ y â‰¡ x âˆ™ kâ‚ â†’ z â‰¡ y âˆ™ kâ‚‚ â†’ z â‰¡ x âˆ™ (kâ‚ âˆ™ kâ‚‚)
  alg-â‰¤-trans {x} {y} {z} {kâ‚} {kâ‚‚} yâ‰¡xâˆ™kâ‚ zâ‰¡yâˆ™kâ‚‚ =
    z             â‰¡âŸ¨ zâ‰¡yâˆ™kâ‚‚ âŸ©
    y âˆ™ kâ‚‚        â‰¡âŸ¨ cong (_âˆ™ kâ‚‚) yâ‰¡xâˆ™kâ‚ âŸ©
    (x âˆ™ kâ‚) âˆ™ kâ‚‚ â‰¡âŸ¨ assoc x kâ‚ kâ‚‚ âŸ©
    x âˆ™ (kâ‚ âˆ™ kâ‚‚) âˆ

  infix 4 _â‰º_
  _â‰º_ : ğ‘† â†’ ğ‘† â†’ Type _
  x â‰º y = âˆƒ[ k ] ((y â‰¡ x âˆ™ k) Ã— (k â‰¢ Îµ))

-- Total Antisymmetric POM
record TAPOM â„“â‚ â„“â‚‚ : Type (â„“suc (â„“â‚ â„“âŠ” â„“â‚‚)) where
  field pom : POM â„“â‚ â„“â‚‚
  open POM pom public using (preorder; _â‰¤_; â‰¤-cong; â‰¤-congÊ³; xâ‰¤xâˆ™y; commutativeMonoid; positive)
  open CommutativeMonoid commutativeMonoid public
  field
    _â‰¤|â‰¥_   : Total _â‰¤_
    antisym : Antisymmetric _â‰¤_

  totalOrder : TotalOrder ğ‘† â„“â‚‚ â„“â‚‚
  totalOrder = fromPartialOrder (record { preorder = preorder ; antisym = antisym }) _â‰¤|â‰¥_
  open TotalOrder totalOrder public hiding (_â‰¤|â‰¥_; antisym) renaming (refl to â‰¤-refl)

-- Every commutative monoid generates a positively ordered monoid
-- called the "algebraic" or "minimal" pom
module AlgebraicPOM {â„“} (mon : CommutativeMonoid â„“) where
  commutativeMonoid = mon
  open CommutativeMonoid mon

  infix 4 _â‰¤_
  _â‰¤_ : ğ‘† â†’ ğ‘† â†’ Type _
  x â‰¤ y = âˆƒ[ z ] (y â‰¡ x âˆ™ z)

  -- The snd here is the same proof as alg-â‰¤-trans, so could be refactored out.
  â‰¤-trans : Transitive _â‰¤_
  â‰¤-trans (kâ‚ , _) (kâ‚‚ , _) .fst = kâ‚ âˆ™ kâ‚‚
  â‰¤-trans {x} {y} {z} (kâ‚ , yâ‰¡xâˆ™kâ‚) (kâ‚‚ , zâ‰¡yâˆ™kâ‚‚) .snd =
    z             â‰¡âŸ¨ zâ‰¡yâˆ™kâ‚‚ âŸ©
    y âˆ™ kâ‚‚        â‰¡âŸ¨ cong (_âˆ™ kâ‚‚) yâ‰¡xâˆ™kâ‚ âŸ©
    (x âˆ™ kâ‚) âˆ™ kâ‚‚ â‰¡âŸ¨ assoc x kâ‚ kâ‚‚ âŸ©
    x âˆ™ (kâ‚ âˆ™ kâ‚‚) âˆ

  preorder : Preorder ğ‘† â„“
  Preorder._â‰¤_ preorder = _â‰¤_
  Preorder.refl preorder = Îµ , sym (âˆ™Îµ _)
  Preorder.trans preorder = â‰¤-trans

  positive : âˆ€ x â†’ Îµ â‰¤ x
  positive x = x , sym (Îµâˆ™ x)

  â‰¤-cong : âˆ€ x {y z} â†’ y â‰¤ z â†’ x âˆ™ y â‰¤ x âˆ™ z
  â‰¤-cong x (k , zâ‰¡yâˆ™k) = k , cong (x âˆ™_) zâ‰¡yâˆ™k Í¾ sym (assoc x _ k)

algebraic-pom : âˆ€ {â„“} â†’ CommutativeMonoid â„“ â†’ POM â„“ â„“
algebraic-pom mon = record { AlgebraicPOM mon }

-- Total Minimal POM
record TMPOM â„“ : Type (â„“suc â„“) where
  field commutativeMonoid : CommutativeMonoid â„“

  pom : POM _ _
  pom = algebraic-pom commutativeMonoid

  open POM pom public

  infix 4 _â‰¤|â‰¥_
  field _â‰¤|â‰¥_ : Total _â‰¤_

  <â‡’â‰º : âˆ€ x y â†’ y â‰° x â†’ x â‰º y
  <â‡’â‰º x y x<y with x â‰¤|â‰¥ y
  ... | inr yâ‰¤x = âŠ¥-elim (x<y yâ‰¤x)
  ... | inl (k , yâ‰¡xâˆ™k) = Î»
    where
    .fst â†’ k
    .snd .fst â†’ yâ‰¡xâˆ™k
    .snd .snd kâ‰¡Îµ â†’ x<y (Îµ , sym (âˆ™Îµ y Í¾ yâ‰¡xâˆ™k Í¾ cong (x âˆ™_) kâ‰¡Îµ Í¾ âˆ™Îµ x))

  infixl 6 _âˆ¸_
  _âˆ¸_ : ğ‘† â†’ ğ‘† â†’ ğ‘†
  x âˆ¸ y = eitherâ€² (const Îµ) fst (x â‰¤|â‰¥ y)

  xâˆ¸yâ‰¤x : âˆ€ x y â†’ x âˆ¸ y â‰¤ x
  xâˆ¸yâ‰¤x x y with x â‰¤|â‰¥ y
  ... | inl (k , p) = positive x
  ... | inr (k , xâ‰¡yâˆ™k) = y , xâ‰¡yâˆ™k Í¾ comm y k

-- Total Minimal Antisymmetric POM
record TMAPOM â„“ : Type (â„“suc â„“) where
  field tmpom : TMPOM â„“
  open TMPOM tmpom public using (_â‰¤_; _â‰¤|â‰¥_; positive; alg-â‰¤-trans; _â‰º_; <â‡’â‰º; _âˆ¸_; xâˆ¸yâ‰¤x)
  field antisym : Antisymmetric _â‰¤_

  tapom : TAPOM _ _
  TAPOM.pom tapom = TMPOM.pom tmpom
  TAPOM._â‰¤|â‰¥_ tapom = _â‰¤|â‰¥_
  TAPOM.antisym tapom = antisym

  open TAPOM tapom public hiding (antisym; _â‰¤|â‰¥_; _â‰¤_; positive)

  zeroSumFree : âˆ€ x y â†’ x âˆ™ y â‰¡ Îµ â†’ x â‰¡ Îµ
  zeroSumFree x y xâˆ™yâ‰¡Îµ = antisym (y , sym xâˆ™yâ‰¡Îµ) (positive x)

  â‰¤â€¿âˆ¸â€¿cancel : âˆ€ x y â†’ x â‰¤ y â†’ (y âˆ¸ x) âˆ™ x â‰¡ y
  â‰¤â€¿âˆ¸â€¿cancel x y xâ‰¤y with y â‰¤|â‰¥ x
  ... | inl yâ‰¤x = Îµâˆ™ x Í¾ antisym xâ‰¤y yâ‰¤x
  ... | inr (k , yâ‰¡xâˆ™k) = comm k x Í¾ sym yâ‰¡xâˆ™k

  âˆ¸â€¿comm : âˆ€ x y â†’ x âˆ™ (y âˆ¸ x) â‰¡ y âˆ™ (x âˆ¸ y)
  âˆ¸â€¿comm x y with y â‰¤|â‰¥ x | x â‰¤|â‰¥ y
  ... | inl yâ‰¤x | inl xâ‰¤y = cong (_âˆ™ Îµ) (antisym xâ‰¤y yâ‰¤x)
  ... | inr (k , yâ‰¡xâˆ™k) | inl xâ‰¤y = sym yâ‰¡xâˆ™k Í¾ sym (âˆ™Îµ y)
  ... | inl yâ‰¤x | inr (k , xâ‰¥y) = âˆ™Îµ x Í¾ xâ‰¥y
  ... | inr (kâ‚ , yâ‰¡xâˆ™kâ‚) | inr (kâ‚‚ , xâ‰¡yâˆ™kâ‚‚) =
    x âˆ™ kâ‚ â‰¡Ë˜âŸ¨ yâ‰¡xâˆ™kâ‚ âŸ©
    y      â‰¡âŸ¨ antisym  (kâ‚‚ , xâ‰¡yâˆ™kâ‚‚) (kâ‚ , yâ‰¡xâˆ™kâ‚) âŸ©
    x      â‰¡âŸ¨ xâ‰¡yâˆ™kâ‚‚ âŸ©
    y âˆ™ kâ‚‚ âˆ

-- Commutative Monoids with Monus
record CMM â„“ : Type (â„“suc â„“) where
  field commutativeMonoid : CommutativeMonoid â„“

  pom : POM _ _
  pom = algebraic-pom commutativeMonoid

  open POM pom public

  field _âˆ¸_ : ğ‘† â†’ ğ‘† â†’ ğ‘†
  infixl 6 _âˆ¸_
  field
    âˆ¸â€¿comm  : âˆ€ x y â†’ x âˆ™ (y âˆ¸ x) â‰¡ y âˆ™ (x âˆ¸ y)
    âˆ¸â€¿assoc : âˆ€ x y z â†’ (x âˆ¸ y) âˆ¸ z â‰¡ x âˆ¸ (y âˆ™ z)
    âˆ¸â€¿inv   : âˆ€ x â†’ x âˆ¸ x â‰¡ Îµ
    Îµâˆ¸      : âˆ€ x â†’ Îµ âˆ¸ x â‰¡ Îµ

  âˆ¸Îµ : âˆ€ x â†’ x âˆ¸ Îµ â‰¡ x
  âˆ¸Îµ x =
    x âˆ¸ Îµ       â‰¡Ë˜âŸ¨ Îµâˆ™ (x âˆ¸ Îµ) âŸ©
    Îµ âˆ™ (x âˆ¸ Îµ) â‰¡âŸ¨ âˆ¸â€¿comm Îµ x âŸ©
    x âˆ™ (Îµ âˆ¸ x) â‰¡âŸ¨ cong (x âˆ™_) (Îµâˆ¸ x) âŸ©
    x âˆ™ Îµ       â‰¡âŸ¨ âˆ™Îµ x âŸ©
    x âˆ

  âˆ¸â‰¤ : âˆ€ x y â†’ x â‰¤ y â†’ x âˆ¸ y â‰¡ Îµ
  âˆ¸â‰¤ x y (k , yâ‰¡xâˆ™k) =
    x âˆ¸ y       â‰¡âŸ¨ cong (x âˆ¸_) yâ‰¡xâˆ™k âŸ©
    x âˆ¸ (x âˆ™ k) â‰¡Ë˜âŸ¨ âˆ¸â€¿assoc x x k âŸ©
    (x âˆ¸ x) âˆ¸ k â‰¡âŸ¨ cong (_âˆ¸ k) (âˆ¸â€¿inv x) âŸ©
    Îµ âˆ¸ k       â‰¡âŸ¨ Îµâˆ¸ k âŸ©
    Îµ âˆ

  âˆ£_-_âˆ£ : ğ‘† â†’ ğ‘† â†’ ğ‘†
  âˆ£ x - y âˆ£ = (x âˆ¸ y) âˆ™ (y âˆ¸ x)

  _âŠ”â‚‚_ : ğ‘† â†’ ğ‘† â†’ ğ‘†
  x âŠ”â‚‚ y = x âˆ™ y âˆ™ âˆ£ x - y âˆ£

  _âŠ“â‚‚_ : ğ‘† â†’ ğ‘† â†’ ğ‘†
  x âŠ“â‚‚ y = (x âˆ™ y) âˆ¸ âˆ£ x - y âˆ£

-- Cancellative Commutative Monoids with Monus
record CCMM â„“ : Type (â„“suc â„“) where
  field cmm : CMM â„“
  open CMM cmm public

  field âˆ¸â€¿cancel : âˆ€ x y â†’ (x âˆ™ y) âˆ¸ x â‰¡ y

  cancelË¡ : CancellativeË¡ _âˆ™_
  cancelË¡ x y z xâˆ™yâ‰¡xâˆ™z =
    y           â‰¡Ë˜âŸ¨ âˆ¸â€¿cancel x y âŸ©
    (x âˆ™ y) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) xâˆ™yâ‰¡xâˆ™z âŸ©
    (x âˆ™ z) âˆ¸ x â‰¡âŸ¨ âˆ¸â€¿cancel x z âŸ©
    z âˆ

  cancelÊ³ : CancellativeÊ³ _âˆ™_
  cancelÊ³ x y z yâˆ™xâ‰¡zâˆ™x =
    y           â‰¡Ë˜âŸ¨ âˆ¸â€¿cancel x y âŸ©
    (x âˆ™ y) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) (comm x y) âŸ©
    (y âˆ™ x) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) yâˆ™xâ‰¡zâˆ™x âŸ©
    (z âˆ™ x) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) (comm z x) âŸ©
    (x âˆ™ z) âˆ¸ x â‰¡âŸ¨ âˆ¸â€¿cancel x z âŸ©
    z âˆ

  zeroSumFree : âˆ€ x y â†’ x âˆ™ y â‰¡ Îµ â†’ x â‰¡ Îµ
  zeroSumFree x y xâˆ™yâ‰¡Îµ =
    x           â‰¡Ë˜âŸ¨ âˆ¸â€¿cancel y x âŸ©
    (y âˆ™ x) âˆ¸ y â‰¡âŸ¨ cong (_âˆ¸ y) (comm y x) âŸ©
    (x âˆ™ y) âˆ¸ y â‰¡âŸ¨ cong (_âˆ¸ y) xâˆ™yâ‰¡Îµ âŸ©
    Îµ âˆ¸ y       â‰¡âŸ¨ Îµâˆ¸ y âŸ©
    Îµ âˆ

  antisym : Antisymmetric _â‰¤_
  antisym {x} {y} (kâ‚ , yâ‰¡xâˆ™kâ‚) (kâ‚‚ , xâ‰¡yâˆ™kâ‚‚) =
      x      â‰¡âŸ¨ xâ‰¡yâˆ™kâ‚‚ âŸ©
      y âˆ™ kâ‚‚ â‰¡âŸ¨ [ lemma                 ]â‡’ y âˆ™ Îµ   â‰¡ y âˆ™ (kâ‚‚ âˆ™ kâ‚)
                âŸ¨ cancelË¡ y Îµ (kâ‚‚ âˆ™ kâ‚) âŸ©â‡’ Îµ       â‰¡ kâ‚‚ âˆ™ kâ‚
                âŸ¨ sym                   âŸ©â‡’ kâ‚‚ âˆ™ kâ‚ â‰¡ Îµ
                âŸ¨ zeroSumFree kâ‚‚ kâ‚     âŸ©â‡’ kâ‚‚      â‰¡ Îµ
                âŸ¨ cong (y âˆ™_)           âŸ©â‡’ y âˆ™ kâ‚‚  â‰¡ y âˆ™ Îµ â‡’âˆ âŸ©
      y âˆ™ Îµ  â‰¡âŸ¨ âˆ™Îµ y âŸ©
      y âˆ
    where
    lemma = âˆ™Îµ y Í¾ alg-â‰¤-trans xâ‰¡yâˆ™kâ‚‚ yâ‰¡xâˆ™kâ‚

  partialOrder : PartialOrder _ _
  PartialOrder.preorder partialOrder = preorder
  PartialOrder.antisym partialOrder = antisym

  â‰ºâ‡’< : âˆ€ x y â†’ x â‰º y â†’ y â‰° x
  â‰ºâ‡’< x y (kâ‚ , yâ‰¡xâˆ™kâ‚ , kâ‚â‰¢Îµ) (kâ‚‚ , xâ‰¡yâˆ™kâ‚‚) =
    [ x âˆ™ Îµ         â‰¡âŸ¨ âˆ™Îµ x âŸ©
      x             â‰¡âŸ¨ xâ‰¡yâˆ™kâ‚‚ âŸ©
      y âˆ™ kâ‚‚        â‰¡âŸ¨ cong (_âˆ™ kâ‚‚) yâ‰¡xâˆ™kâ‚ âŸ©
      (x âˆ™ kâ‚) âˆ™ kâ‚‚ â‰¡âŸ¨ assoc x kâ‚ kâ‚‚ âŸ©
      x âˆ™ (kâ‚ âˆ™ kâ‚‚) âˆ       ]â‡’ x âˆ™ Îµ â‰¡ x âˆ™ (kâ‚ âˆ™ kâ‚‚)
    âŸ¨ cancelË¡ x Îµ (kâ‚ âˆ™ kâ‚‚) âŸ©â‡’ Îµ â‰¡ kâ‚ âˆ™ kâ‚‚
    âŸ¨ sym                   âŸ©â‡’ kâ‚ âˆ™ kâ‚‚ â‰¡Â Îµ
    âŸ¨ zeroSumFree kâ‚ kâ‚‚     âŸ©â‡’ kâ‚ â‰¡ Îµ
    âŸ¨ kâ‚â‰¢Îµ                  âŸ©â‡’ âŠ¥ â‡’âˆ

  â‰¤â‡’<â‡’â‰¢Îµ : âˆ€ x y â†’ (xâ‰¤y : x â‰¤ y) â†’ y â‰° x â†’ fst xâ‰¤y â‰¢ Îµ
  â‰¤â‡’<â‡’â‰¢Îµ x y (kâ‚ , yâ‰¡xâˆ™kâ‚) yâ‰°x kâ‚â‰¡Îµ = yâ‰°x Î»
    where
    .fst â†’ Îµ
    .snd â†’ x      â‰¡Ë˜âŸ¨ âˆ™Îµ x âŸ©
           x âˆ™ Îµ  â‰¡Ë˜âŸ¨ cong (x âˆ™_) kâ‚â‰¡Îµ âŸ©
           x âˆ™ kâ‚ â‰¡Ë˜âŸ¨ yâ‰¡xâˆ™kâ‚ âŸ©
           y      â‰¡Ë˜âŸ¨ âˆ™Îµ y âŸ© 
           y âˆ™ Îµ âˆ

  â‰¤-cancelÊ³ : âˆ€ x y z â†’ y âˆ™ x â‰¤ z âˆ™ x â†’ y â‰¤ z
  â‰¤-cancelÊ³ x y z (k , zâˆ™xâ‰¡yâˆ™xâˆ™k) .fst = k
  â‰¤-cancelÊ³ x y z (k , zâˆ™xâ‰¡yâˆ™xâˆ™k) .snd = cancelÊ³ x z (y âˆ™ k) $
    z âˆ™ x â‰¡âŸ¨ zâˆ™xâ‰¡yâˆ™xâˆ™k âŸ©
    (y âˆ™ x) âˆ™ k â‰¡âŸ¨ assoc y x k âŸ©
    y âˆ™ (x âˆ™ k) â‰¡âŸ¨ cong (y âˆ™_) (comm x k) âŸ©
    y âˆ™ (k âˆ™ x) â‰¡Ë˜âŸ¨ assoc y k x âŸ©
    (y âˆ™ k) âˆ™ x âˆ

  â‰¤-cancelË¡ : âˆ€ x y z â†’ x âˆ™ y â‰¤ x âˆ™ z â†’ y â‰¤ z
  â‰¤-cancelË¡ x y z (k , xâˆ™zâ‰¡xâˆ™yâˆ™k) .fst = k
  â‰¤-cancelË¡ x y z (k , xâˆ™zâ‰¡xâˆ™yâˆ™k) .snd =
    cancelË¡ x z (y âˆ™ k) (xâˆ™zâ‰¡xâˆ™yâˆ™k Í¾ assoc x y k)

-- Cancellative total minimal antisymmetric pom (has monus)
record CTMAPOM â„“ : Type (â„“suc â„“) where
  field tmapom : TMAPOM â„“
  open TMAPOM tmapom public
  field cancel : CancellativeË¡ _âˆ™_

  module cmm where
    âˆ¸â‰¤ : âˆ€ x y â†’ x â‰¤ y â†’ x âˆ¸ y â‰¡ Îµ
    âˆ¸â‰¤ x y xâ‰¤y with x â‰¤|â‰¥ y
    âˆ¸â‰¤ x y xâ‰¤y | inl _ = refl
    âˆ¸â‰¤ x y (kâ‚ , yâ‰¡xâˆ™kâ‚) | inr (kâ‚‚ , xâ‰¡yâˆ™kâ‚‚) =
      [ lemma                ]â‡’ y âˆ™ Îµ   â‰¡ y âˆ™ (kâ‚‚ âˆ™ kâ‚)
      âŸ¨ cancel y Îµ (kâ‚‚ âˆ™ kâ‚) âŸ©â‡’ Îµ       â‰¡ kâ‚‚ âˆ™ kâ‚
      âŸ¨ sym                  âŸ©â‡’ kâ‚‚ âˆ™ kâ‚ â‰¡ Îµ
      âŸ¨ zeroSumFree kâ‚‚ kâ‚    âŸ©â‡’ kâ‚‚      â‰¡ Îµ â‡’âˆ
      where
      lemma = âˆ™Îµ y Í¾ alg-â‰¤-trans xâ‰¡yâˆ™kâ‚‚ yâ‰¡xâˆ™kâ‚

    âˆ¸â€¿inv : âˆ€ x â†’ x âˆ¸ x â‰¡ Îµ
    âˆ¸â€¿inv x = âˆ¸â‰¤ x x â‰¤-refl

    Îµâˆ¸ : âˆ€ x â†’ Îµ âˆ¸ x â‰¡ Îµ
    Îµâˆ¸ x = âˆ¸â‰¤ Îµ x (positive x)

    âˆ¸â€¿assoc : âˆ€ x y z â†’ (x âˆ¸ y) âˆ¸ z â‰¡ x âˆ¸ (y âˆ™ z)
    âˆ¸â€¿assoc x y z with x â‰¤|â‰¥ y
    âˆ¸â€¿assoc x y z | inl xâ‰¤y  = Îµâˆ¸ z Í¾ sym (âˆ¸â‰¤ x (y âˆ™ z) (â‰¤-trans xâ‰¤y xâ‰¤xâˆ™y))
    âˆ¸â€¿assoc x y z | inr xâ‰¥y with x â‰¤|â‰¥ y âˆ™ z
    âˆ¸â€¿assoc x y z | inr (kâ‚ , xâ‰¡yâˆ™kâ‚) | inl (kâ‚‚ , yâˆ™zâ‰¡xâˆ™kâ‚‚) = âˆ¸â‰¤ kâ‚ z (kâ‚‚ , lemma)
      where
      lemma : z â‰¡ kâ‚ âˆ™ kâ‚‚
      lemma = cancel y z (kâ‚ âˆ™ kâ‚‚) (alg-â‰¤-trans xâ‰¡yâˆ™kâ‚ yâˆ™zâ‰¡xâˆ™kâ‚‚)

    âˆ¸â€¿assoc x y z | inr (k , xâ‰¡yâˆ™k) | inr xâ‰¥yâˆ™z with k â‰¤|â‰¥ z
    âˆ¸â€¿assoc x y z | inr (kâ‚ , xâ‰¡yâˆ™kâ‚) | inr (kâ‚‚ , xâ‰¡yâˆ™zâˆ™kâ‚‚) | inl (kâ‚ƒ , zâ‰¡kâ‚âˆ™kâ‚ƒ) =
        [ lemmaâ‚            ]â‡’ Îµ       â‰¡ kâ‚‚ âˆ™ kâ‚ƒ
        âŸ¨ sym               âŸ©â‡’ kâ‚‚ âˆ™ kâ‚ƒ â‰¡ Îµ
        âŸ¨ zeroSumFree kâ‚‚ kâ‚ƒ âŸ©â‡’ kâ‚‚      â‰¡ Îµ
        âŸ¨ sym               âŸ©â‡’ Îµ       â‰¡ kâ‚‚ â‡’âˆ
      where
      lemmaâ‚ƒ =
        y âˆ™ kâ‚       â‰¡Ë˜âŸ¨ xâ‰¡yâˆ™kâ‚ âŸ©
        x            â‰¡âŸ¨ xâ‰¡yâˆ™zâˆ™kâ‚‚ âŸ©
        (y âˆ™ z) âˆ™ kâ‚‚ â‰¡âŸ¨ assoc y z kâ‚‚ âŸ©
        y âˆ™ (z âˆ™ kâ‚‚) âˆ

      lemmaâ‚‚ =
        kâ‚ âˆ™ Îµ         â‰¡âŸ¨ âˆ™Îµ kâ‚ âŸ©
        kâ‚             â‰¡âŸ¨ alg-â‰¤-trans zâ‰¡kâ‚âˆ™kâ‚ƒ (cancel y kâ‚ (z âˆ™ kâ‚‚) lemmaâ‚ƒ) âŸ©
        kâ‚ âˆ™ (kâ‚ƒ âˆ™ kâ‚‚) âˆ

      lemmaâ‚ =
        Îµ       â‰¡âŸ¨ cancel kâ‚ Îµ (kâ‚ƒ âˆ™ kâ‚‚) lemmaâ‚‚ âŸ©
        kâ‚ƒ âˆ™ kâ‚‚ â‰¡âŸ¨ comm kâ‚ƒ kâ‚‚ âŸ©
        kâ‚‚ âˆ™ kâ‚ƒ âˆ

    âˆ¸â€¿assoc x y z | inr (kâ‚ , xâ‰¡yâˆ™kâ‚) | inr (kâ‚‚ , xâ‰¡yâˆ™zâˆ™kâ‚‚) | inr (kâ‚ƒ , kâ‚â‰¡zâˆ™kâ‚ƒ) =
        cancel z kâ‚ƒ kâ‚‚ lemmaâ‚‚
      where
      lemmaâ‚ =
        y âˆ™ kâ‚       â‰¡Ë˜âŸ¨ xâ‰¡yâˆ™kâ‚ âŸ©
        x            â‰¡âŸ¨ xâ‰¡yâˆ™zâˆ™kâ‚‚ âŸ©
        (y âˆ™ z) âˆ™ kâ‚‚ â‰¡âŸ¨ assoc y z kâ‚‚ âŸ©
        y âˆ™ (z âˆ™ kâ‚‚) âˆ

      lemmaâ‚‚ =
        z âˆ™ kâ‚ƒ â‰¡Ë˜âŸ¨ kâ‚â‰¡zâˆ™kâ‚ƒ âŸ©
        kâ‚     â‰¡âŸ¨ cancel y kâ‚ (z âˆ™ kâ‚‚) lemmaâ‚ âŸ©
        z âˆ™ kâ‚‚ âˆ

  open cmm public

  âˆ¸â€¿cancel : âˆ€ x y â†’ (x âˆ™ y) âˆ¸ x â‰¡ y
  âˆ¸â€¿cancel x y with (x âˆ™ y) â‰¤|â‰¥ x
  ... | inl xâˆ™yâ‰¤x = sym (cancel x y Îµ (antisym xâˆ™yâ‰¤x xâ‰¤xâˆ™y Í¾ sym (âˆ™Îµ x)))
  ... | inr (k , xâˆ™yâ‰¡xâˆ™k) = sym (cancel x y k xâˆ™yâ‰¡xâˆ™k)

  ccmm : CCMM _
  ccmm = record { âˆ¸â€¿cancel = âˆ¸â€¿cancel
                ; cmm = record { cmm
                               ; âˆ¸â€¿comm = âˆ¸â€¿comm
                               ; commutativeMonoid = commutativeMonoid } }

  open CCMM ccmm public
    using (cancelÊ³; cancelË¡; âˆ¸Îµ; â‰ºâ‡’<; â‰¤â‡’<â‡’â‰¢Îµ; _âŠ”â‚‚_; _âŠ“â‚‚_)

  2Ã— : ğ‘† â†’ ğ‘†
  2Ã— x = x âˆ™ x

  open import Relation.Binary.Lattice totalOrder

  double-max : âˆ€ x y â†’ 2Ã— (x âŠ” y) â‰¡ x âŠ”â‚‚ y
  double-max x y with x â‰¤|â‰¥ y | y â‰¤|â‰¥ x
  double-max x y | inl xâ‰¤y | inl yâ‰¤x =
    x âˆ™ x â‰¡âŸ¨ cong (x âˆ™_) (antisym xâ‰¤y yâ‰¤x) âŸ©
    x âˆ™ y â‰¡Ë˜âŸ¨ âˆ™Îµ (x âˆ™ y) âŸ©
    (x âˆ™ y) âˆ™ Îµ â‰¡Ë˜âŸ¨ cong ((x âˆ™ y) âˆ™_)  (Îµâˆ™ Îµ) âŸ©
    (x âˆ™ y) âˆ™ (Îµ âˆ™ Îµ) âˆ
  double-max x y | inl xâ‰¤y | inr (k , yâ‰¡xâˆ™k) =
    y âˆ™ y â‰¡âŸ¨ cong (y âˆ™_) yâ‰¡xâˆ™k âŸ©
    y âˆ™ (x âˆ™ k) â‰¡Ë˜âŸ¨ assoc y x k âŸ©
    (y âˆ™ x) âˆ™ k â‰¡âŸ¨ cong (_âˆ™ k) (comm y x) âŸ©
    (x âˆ™ y) âˆ™ k â‰¡Ë˜âŸ¨ cong ((x âˆ™ y) âˆ™_) (Îµâˆ™ k) âŸ©
    (x âˆ™ y) âˆ™ (Îµ âˆ™ k) âˆ
  double-max x y | inr (k , xâ‰¡yâˆ™k) | inl yâ‰¤x =
    x âˆ™ x â‰¡âŸ¨ cong (x âˆ™_) xâ‰¡yâˆ™k âŸ©
    x âˆ™ (y âˆ™ k) â‰¡Ë˜âŸ¨ assoc x y k âŸ©
    (x âˆ™ y) âˆ™ k â‰¡Ë˜âŸ¨ cong ((x âˆ™ y) âˆ™_) (âˆ™Îµ k) âŸ©
    (x âˆ™ y) âˆ™ (k âˆ™ Îµ) âˆ
  double-max x y | inr (kâ‚ , xâ‰¡yâˆ™kâ‚) | inr (kâ‚‚ , yâ‰¡xâˆ™kâ‚‚) =
    x âˆ™ x â‰¡âŸ¨ cong (x âˆ™_) (antisym (kâ‚‚ , yâ‰¡xâˆ™kâ‚‚) (kâ‚ , xâ‰¡yâˆ™kâ‚)) âŸ©
    x âˆ™ y â‰¡âŸ¨ congâ‚‚ _âˆ™_ xâ‰¡yâˆ™kâ‚ yâ‰¡xâˆ™kâ‚‚ âŸ©
    (y âˆ™ kâ‚) âˆ™ (x âˆ™ kâ‚‚) â‰¡Ë˜âŸ¨ assoc (y âˆ™ kâ‚) x kâ‚‚ âŸ©
    ((y âˆ™ kâ‚) âˆ™ x) âˆ™ kâ‚‚ â‰¡âŸ¨ cong (_âˆ™ kâ‚‚) (comm (y âˆ™ kâ‚) x) âŸ©
    (x âˆ™ (y âˆ™ kâ‚)) âˆ™ kâ‚‚ â‰¡Ë˜âŸ¨ cong (_âˆ™ kâ‚‚) (assoc x y kâ‚) âŸ©
    ((x âˆ™ y) âˆ™ kâ‚) âˆ™ kâ‚‚ â‰¡âŸ¨ assoc (x âˆ™ y) kâ‚ kâ‚‚ âŸ©
    (x âˆ™ y) âˆ™ (kâ‚ âˆ™ kâ‚‚) âˆ

  open import Data.Sigma.Properties

  â‰¤-prop : âˆ€ x y â†’ isProp (x â‰¤ y)
  â‰¤-prop x y (kâ‚ , yâ‰¡xâˆ™kâ‚) (kâ‚‚ , yâ‰¡xâˆ™kâ‚‚) = Î£â‰¡Prop (Î» _ â†’ totalâ‡’isSet _ _) (cancelË¡ x kâ‚ kâ‚‚ (sym yâ‰¡xâˆ™kâ‚ Í¾ yâ‰¡xâˆ™kâ‚‚))

-- We can construct the viterbi semiring by adjoining a top element to
-- a tapom
module Viterbi {â„“â‚} {â„“â‚‚} (tapom : TAPOM â„“â‚ â„“â‚‚) where
  open TAPOM tapom
  open import Relation.Binary.Construct.UpperBound totalOrder
  open import Relation.Binary.Lattice totalOrder

  âŸ¨âŠ“âŸ©âˆ™ : _âˆ™_ DistributesË¡ _âŠ“_
  âŸ¨âŠ“âŸ©âˆ™ x y z with x <? y | (x âˆ™ z) <? (y âˆ™ z)
  ... | yes x<y | yes xz<yz = refl
  ... | no  xâ‰®y | no  xzâ‰®yz = refl
  ... | no  xâ‰®y | yes xz<yz = âŠ¥-elim (<â‡’â‰± xz<yz (â‰¤-congÊ³ z (â‰®â‡’â‰¥ xâ‰®y)))
  ... | yes x<y | no  xzâ‰®yz = antisym (â‰¤-congÊ³ z (<â‡’â‰¤ x<y)) (â‰®â‡’â‰¥ xzâ‰®yz)

  âˆ™âŸ¨âŠ“âŸ© : _âˆ™_ DistributesÊ³ _âŠ“_
  âˆ™âŸ¨âŠ“âŸ© x y z = comm x (y âŠ“ z) Í¾ âŸ¨âŠ“âŸ©âˆ™ y z x Í¾ congâ‚‚ _âŠ“_ (comm y x) (comm z x)

  open UBSugar

  module NS where
    ğ‘… = âŒˆâˆ™âŒ‰

    0# 1# : ğ‘…
    _*_ _+_ : ğ‘… â†’ ğ‘… â†’ ğ‘…

    1# = âŒˆ Îµ âŒ‰

    âŒˆâŠ¤âŒ‰   * y     = âŒˆâŠ¤âŒ‰
    âŒˆ x âŒ‰ * âŒˆâŠ¤âŒ‰   = âŒˆâŠ¤âŒ‰
    âŒˆ x âŒ‰ * âŒˆ y âŒ‰ = âŒˆ x âˆ™ y âŒ‰

    0# = âŒˆâŠ¤âŒ‰

    âŒˆâŠ¤âŒ‰   + y     = y
    âŒˆ x âŒ‰ + âŒˆâŠ¤âŒ‰   = âŒˆ x âŒ‰
    âŒˆ x âŒ‰ + âŒˆ y âŒ‰ = âŒˆ x âŠ“ y âŒ‰

    *-assoc : Associative _*_
    *-assoc âŒˆâŠ¤âŒ‰   _     _     = refl
    *-assoc âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰   _     = refl
    *-assoc âŒˆ _ âŒ‰ âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰   = refl
    *-assoc âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆ z âŒ‰ = cong âŒˆ_âŒ‰ (assoc x y z)

    *-com : Commutative _*_
    *-com âŒˆâŠ¤âŒ‰   âŒˆâŠ¤âŒ‰   = refl
    *-com âŒˆâŠ¤âŒ‰   âŒˆ _ âŒ‰ = refl
    *-com âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰   = refl
    *-com âŒˆ x âŒ‰ âŒˆ y âŒ‰ = cong âŒˆ_âŒ‰ (comm x y)

    âŸ¨+âŸ©* : _*_ DistributesË¡ _+_
    âŸ¨+âŸ©* âŒˆâŠ¤âŒ‰   _     _     = refl
    âŸ¨+âŸ©* âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰   âŒˆâŠ¤âŒ‰   = refl
    âŸ¨+âŸ©* âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰   âŒˆ _ âŒ‰ = refl
    âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆâŠ¤âŒ‰   = refl
    âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆ z âŒ‰ = cong âŒˆ_âŒ‰ (âŸ¨âŠ“âŸ©âˆ™ x y z)

    +-assoc : Associative _+_
    +-assoc âŒˆâŠ¤âŒ‰   _     _     = refl
    +-assoc âŒˆ x âŒ‰ âŒˆâŠ¤âŒ‰   _     = refl
    +-assoc âŒˆ x âŒ‰ âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰   = refl
    +-assoc âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆ z âŒ‰ = cong âŒˆ_âŒ‰ (âŠ“-assoc x y z)

    0+ : âˆ€ x â†’ 0# + x â‰¡ x
    0+ âŒˆâŠ¤âŒ‰   = refl
    0+ âŒˆ _ âŒ‰ = refl

    +0 : âˆ€ x â†’ x + 0# â‰¡ x
    +0 âŒˆ _ âŒ‰ = refl
    +0 âŒˆâŠ¤âŒ‰   = refl

    1* : âˆ€ x â†’ 1# * x â‰¡ x
    1* âŒˆâŠ¤âŒ‰   = refl
    1* âŒˆ x âŒ‰ = cong âŒˆ_âŒ‰ (Îµâˆ™ x)

    *1 : âˆ€ x â†’ x * 1# â‰¡ x
    *1 âŒˆâŠ¤âŒ‰   = refl
    *1 âŒˆ x âŒ‰ = cong âŒˆ_âŒ‰ (âˆ™Îµ x)

    0* : âˆ€ x â†’ 0# * x â‰¡ 0#
    0* _ = refl

  open NS

  nearSemiring : NearSemiring _
  nearSemiring = record { NS }

  +-comm : Commutative _+_
  +-comm âŒˆâŠ¤âŒ‰   âŒˆâŠ¤âŒ‰   = refl
  +-comm âŒˆâŠ¤âŒ‰   âŒˆ _ âŒ‰ = refl
  +-comm âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰   = refl
  +-comm âŒˆ x âŒ‰ âŒˆ y âŒ‰ = cong âŒˆ_âŒ‰ (âŠ“-comm x y)

  *0 : âˆ€ x â†’ x * âŒˆâŠ¤âŒ‰ â‰¡ âŒˆâŠ¤âŒ‰
  *0 âŒˆ _ âŒ‰ = refl
  *0 âŒˆâŠ¤âŒ‰   = refl

  *âŸ¨+âŸ© : _*_ DistributesÊ³ _+_
  *âŸ¨+âŸ© x y z = *-com x (y + z) Í¾ âŸ¨+âŸ©* y z x Í¾ congâ‚‚ _+_ (*-com y x) (*-com z x)

viterbi : âˆ€ {â„“â‚ â„“â‚‚} â†’ TAPOM â„“â‚ â„“â‚‚ â†’ Semiring â„“â‚
viterbi tapom = record { Viterbi tapom }
