{-# OPTIONS --cubical --safe #-}

module Algebra.Monus where

open import Prelude
open import Algebra
open import Relation.Binary
open import Path.Reasoning

-- Positively ordered monoids
record POM â„“â‚ â„“â‚‚ : Type (â„“suc (â„“â‚ â„“âŠ” â„“â‚‚)) where
  field commutativeMonoid : CommutativeMonoid â„“â‚
  open CommutativeMonoid commutativeMonoid public
  field preorder : Preorder ğ‘† â„“â‚‚
  open Preorder preorder public
  field
    positive : âˆ€ x â†’ Îµ â‰¤ x
    â‰¤-cong : âˆ€ x {y z} â†’ y â‰¤ z â†’ x âˆ™ y â‰¤ x âˆ™ z

  xâ‰¤xâˆ™y : âˆ€ {x y} â†’ x â‰¤ x âˆ™ y
  xâ‰¤xâˆ™y {x} {y} = subst (_â‰¤ x âˆ™ y) (âˆ™Îµ x) (â‰¤-cong x (positive y))

  â‰¤-congÊ³ : âˆ€ x {y z} â†’ y â‰¤ z â†’ y âˆ™ x â‰¤ z âˆ™ x
  â‰¤-congÊ³ x {y} {z} p = subst (y âˆ™ x â‰¤_) (comm x z) (subst (_â‰¤ x âˆ™ z) (comm x y) (â‰¤-cong x p))

-- Total Antisymmetric POM
record TAPOM â„“â‚ â„“â‚‚ : Type (â„“suc (â„“â‚ â„“âŠ” â„“â‚‚)) where
  field pom : POM â„“â‚ â„“â‚‚
  open POM pom public using (preorder; _â‰¤_; â‰¤-cong; â‰¤-congÊ³; xâ‰¤xâˆ™y; commutativeMonoid)
  open CommutativeMonoid commutativeMonoid public
  field
    _â‰¤|â‰¥_   : Total _â‰¤_
    antisym : Antisymmetric _â‰¤_

  totalOrder : TotalOrder ğ‘† â„“â‚‚ â„“â‚‚
  totalOrder = fromPartialOrder (record { preorder = preorder ; antisym = antisym }) _â‰¤|â‰¥_
  open TotalOrder totalOrder public hiding (_â‰¤|â‰¥_; antisym; refl)

  module Viterbi where
    open import Relation.Binary.Construct.UpperBound totalOrder
    open import Relation.Binary.Lattice ub-ord
    open UBSugar

    module NS where
      _*_ : âŒˆâˆ™âŒ‰ â†’ âŒˆâˆ™âŒ‰ â†’ âŒˆâˆ™âŒ‰
      x * y = â¦‡ x âˆ™ y â¦ˆ

      *-assoc : Associative _*_
      *-assoc âŒˆâŠ¤âŒ‰ âŒˆâŠ¤âŒ‰ âŒˆâŠ¤âŒ‰ = refl
      *-assoc âŒˆâŠ¤âŒ‰ âŒˆâŠ¤âŒ‰ âŒˆ x âŒ‰ = refl
      *-assoc âŒˆâŠ¤âŒ‰ âŒˆ x âŒ‰ âŒˆâŠ¤âŒ‰ = refl
      *-assoc âŒˆâŠ¤âŒ‰ âŒˆ _ âŒ‰ âŒˆ _ âŒ‰ = refl
      *-assoc âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰ âŒˆâŠ¤âŒ‰ = refl
      *-assoc âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰ âŒˆ _ âŒ‰ = refl
      *-assoc âŒˆ _ âŒ‰ âŒˆ _ âŒ‰ âŒˆâŠ¤âŒ‰ = refl
      *-assoc âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆ z âŒ‰ = cong âŒˆ_âŒ‰ (assoc x y z)

      *-com : Commutative _*_
      *-com âŒˆâŠ¤âŒ‰   âŒˆâŠ¤âŒ‰ = refl
      *-com âŒˆâŠ¤âŒ‰   âŒˆ x âŒ‰ = refl
      *-com âŒˆ x âŒ‰ âŒˆâŠ¤âŒ‰ = refl
      *-com âŒˆ x âŒ‰ âŒˆ y âŒ‰ = cong âŒˆ_âŒ‰ (comm x y)

      âŸ¨+âŸ©* : _*_ DistributesË¡ _âŠ“_
      âŸ¨+âŸ©* âŒˆâŠ¤âŒ‰ _ _ = refl
      âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆâŠ¤âŒ‰ âŒˆâŠ¤âŒ‰ = refl
      âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆâŠ¤âŒ‰ âŒˆ z âŒ‰ = refl
      âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆâŠ¤âŒ‰ = *-com _ _
      âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆ z âŒ‰ with x <? y | (x âˆ™ z) <? (y âˆ™ z)
      âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆ z âŒ‰ | yes x<y | yes xz<yz = refl
      âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆ z âŒ‰ | no  xâ‰®y | no  xzâ‰®yz = refl
      âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆ z âŒ‰ | no  xâ‰®y | yes xz<yz = âŠ¥-elim (<â‡’â‰± xz<yz (â‰¤-congÊ³ z (â‰®â‡’â‰¥ xâ‰®y)))
      âŸ¨+âŸ©* âŒˆ x âŒ‰ âŒˆ y âŒ‰ âŒˆ z âŒ‰ | yes x<y | no  xzâ‰®yz = TotalOrder.antisym ub-ord (â‰¤-congÊ³ z (<â‡’â‰¤ x<y)) (â‰®â‡’â‰¥ xzâ‰®yz)

      ğ‘… = âŒˆâˆ™âŒ‰

      1# = âŒˆ Îµ âŒ‰
      0# = âŒˆâŠ¤âŒ‰

      +-assoc = âŠ“-assoc

      0+ : âˆ€ x â†’ âŒˆâŠ¤âŒ‰ âŠ“ x â‰¡ x
      0+ âŒˆâŠ¤âŒ‰ = refl
      0+ âŒˆ x âŒ‰ = refl

      +0 : âˆ€ x â†’ x âŠ“ âŒˆâŠ¤âŒ‰ â‰¡ x
      +0 âŒˆ x âŒ‰ = refl
      +0 âŒˆâŠ¤âŒ‰ = refl

      1* : âˆ€ x â†’ âŒˆ Îµ âŒ‰ * x â‰¡ x
      1* âŒˆâŠ¤âŒ‰ = refl
      1* âŒˆ x âŒ‰ = cong âŒˆ_âŒ‰ (Îµâˆ™ x)

      *1 : âˆ€ x â†’ x * âŒˆ Îµ âŒ‰ â‰¡ x
      *1 âŒˆâŠ¤âŒ‰ = refl
      *1 âŒˆ x âŒ‰ = cong âŒˆ_âŒ‰ (âˆ™Îµ x)

      0* : âˆ€ x â†’ 0# * x â‰¡ 0#
      0* x = refl

    nearSemiring : NearSemiring _
    nearSemiring = record { NS ; _+_ = _âŠ“_ }

    +-comm = âŠ“-comm
    open NS

    *0 : âˆ€ x â†’ x * âŒˆâŠ¤âŒ‰ â‰¡ âŒˆâŠ¤âŒ‰
    *0 âŒˆ x âŒ‰ = refl
    *0 âŒˆâŠ¤âŒ‰ = refl

    *âŸ¨+âŸ© : _*_ DistributesÊ³ _âŠ“_
    *âŸ¨+âŸ© x y z = *-com x (y âŠ“ z) Í¾ âŸ¨+âŸ©* y z x Í¾ congâ‚‚ _âŠ“_ (*-com y x) (*-com z x)

  viterbi : Semiring â„“â‚
  viterbi = record { Viterbi }

-- Every commutative monoid generates a positively ordered monoid
-- called the "algebraic" or "minimal" pom
module AlgebraicPOM {â„“} (mon : CommutativeMonoid â„“) where
  commutativeMonoid = mon
  open CommutativeMonoid mon

  infix 4 _â‰¤_
  _â‰¤_ : ğ‘† â†’ ğ‘† â†’ Type _
  x â‰¤ y = âˆƒ[ z ] (y â‰¡ x âˆ™ z)

  â‰¤-trans : Transitive _â‰¤_
  â‰¤-trans (kâ‚ , _) (kâ‚‚ , _) .fst = kâ‚ âˆ™ kâ‚‚
  â‰¤-trans {x} {y} {z} (kâ‚ , yâ‰¡xâˆ™kâ‚) (kâ‚‚ , zâ‰¡yâˆ™kâ‚‚) .snd =
    z â‰¡âŸ¨ zâ‰¡yâˆ™kâ‚‚ âŸ©
    y âˆ™ kâ‚‚ â‰¡âŸ¨ cong (_âˆ™ kâ‚‚) yâ‰¡xâˆ™kâ‚ âŸ©
    (x âˆ™ kâ‚) âˆ™ kâ‚‚ â‰¡âŸ¨ assoc x kâ‚ kâ‚‚ âŸ©
    x âˆ™ (kâ‚ âˆ™ kâ‚‚) âˆ

  preorder : Preorder ğ‘† â„“
  Preorder._â‰¤_ preorder = _â‰¤_
  Preorder.refl preorder = Îµ , sym (âˆ™Îµ _)
  Preorder.trans preorder = â‰¤-trans

  positive : âˆ€ x â†’ Îµ â‰¤ x
  positive x = x , sym (Îµâˆ™ x)

  â‰¤-cong : âˆ€ x {y z} â†’ y â‰¤ z â†’ x âˆ™ y â‰¤ x âˆ™ z
  â‰¤-cong x (k , zâ‰¡yâˆ™k) = k , cong (x âˆ™_) zâ‰¡yâˆ™k Í¾ sym (assoc x _ k)

algebraic-pom : âˆ€ {â„“} â†’ CommutativeMonoid â„“ â†’ POM â„“ â„“
algebraic-pom mon = record { AlgebraicPOM mon }

-- Total Minimal POM
record TMPOM â„“ : Type (â„“suc â„“) where
  field commutativeMonoid : CommutativeMonoid â„“

  pom : POM _ _
  pom = algebraic-pom commutativeMonoid

  open POM pom public hiding (refl)

  infix 4 _â‰¤|â‰¥_
  field _â‰¤|â‰¥_ : Total _â‰¤_

-- Total Minimal Antisymmetric POM
record TMAPOM â„“ : Type (â„“suc â„“) where
  field tmpom : TMPOM â„“
  open TMPOM tmpom public using (_â‰¤_; _â‰¤|â‰¥_; positive)
  field antisym : Antisymmetric _â‰¤_

  tapom : TAPOM _ _
  TAPOM.pom tapom = TMPOM.pom tmpom
  TAPOM._â‰¤|â‰¥_ tapom = _â‰¤|â‰¥_
  TAPOM.antisym tapom = antisym

  open TAPOM tapom public hiding (antisym; _â‰¤|â‰¥_)

  zeroSumFree : âˆ€ x y â†’ x âˆ™ y â‰¡ Îµ â†’ x â‰¡ Îµ
  zeroSumFree x y xâˆ™yâ‰¡Îµ = antisym (y , sym xâˆ™yâ‰¡Îµ) (positive x)

-- Commutative Monoids with Monus
record CMM â„“ : Type (â„“suc â„“) where
  field commutativeMonoid : CommutativeMonoid â„“
  open CommutativeMonoid commutativeMonoid public

  field _âˆ¸_ : ğ‘† â†’ ğ‘† â†’ ğ‘†
  infixl 6 _âˆ¸_
  field
    âˆ¸â€¿comm  : âˆ€ x y â†’ x âˆ™ (y âˆ¸ x) â‰¡ y âˆ™ (x âˆ¸ y)
    âˆ¸â€¿assoc : âˆ€ x y z â†’ (x âˆ¸ y) âˆ¸ z â‰¡ x âˆ¸ (y âˆ™ z)
    âˆ¸â€¿inv   : âˆ€ x â†’ x âˆ¸ x â‰¡ Îµ
    Îµâˆ¸      : âˆ€ x â†’ Îµ âˆ¸ x â‰¡ Îµ

  open import Path.Reasoning

  âˆ¸Îµ : âˆ€ x â†’ x âˆ¸ Îµ â‰¡ x
  âˆ¸Îµ x =
    x âˆ¸ Îµ       â‰¡Ë˜âŸ¨ Îµâˆ™ (x âˆ¸ Îµ) âŸ©
    Îµ âˆ™ (x âˆ¸ Îµ) â‰¡âŸ¨ âˆ¸â€¿comm Îµ x âŸ©
    x âˆ™ (Îµ âˆ¸ x) â‰¡âŸ¨ cong (x âˆ™_) (Îµâˆ¸ x) âŸ©
    x âˆ™ Îµ       â‰¡âŸ¨ âˆ™Îµ x âŸ©
    x âˆ

-- Cancellative Commutative Monoids with Monus
record CCMM â„“ : Type (â„“suc â„“) where
  field cmm : CMM â„“
  open CMM cmm public

  field âˆ¸â€¿cancel : âˆ€ x y â†’ (x âˆ™ y) âˆ¸ x â‰¡ y

  open import Path.Reasoning

  cancelË¡ : CancellativeË¡ _âˆ™_
  cancelË¡ x y z xâˆ™yâ‰¡xâˆ™z =
    y           â‰¡Ë˜âŸ¨ âˆ¸â€¿cancel x y âŸ©
    (x âˆ™ y) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) xâˆ™yâ‰¡xâˆ™z âŸ©
    (x âˆ™ z) âˆ¸ x â‰¡âŸ¨ âˆ¸â€¿cancel x z âŸ©
    z âˆ

  cancelÊ³ : CancellativeÊ³ _âˆ™_
  cancelÊ³ x y z yâˆ™xâ‰¡zâˆ™x =
    y           â‰¡Ë˜âŸ¨ âˆ¸â€¿cancel x y âŸ©
    (x âˆ™ y) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) (comm x y) âŸ©
    (y âˆ™ x) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) yâˆ™xâ‰¡zâˆ™x âŸ©
    (z âˆ™ x) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) (comm z x) âŸ©
    (x âˆ™ z) âˆ¸ x â‰¡âŸ¨ âˆ¸â€¿cancel x z âŸ©
    z âˆ

  pom : POM _ _
  pom = algebraic-pom commutativeMonoid

  open POM pom public hiding (semigroup; commutativeMonoid; monoid; _âˆ™_; Îµ; assoc; comm; Îµâˆ™; âˆ™Îµ)

  zeroSumFree : âˆ€ x y â†’ x âˆ™ y â‰¡ Îµ â†’ x â‰¡ Îµ
  zeroSumFree x y xâˆ™yâ‰¡Îµ =
    x           â‰¡Ë˜âŸ¨ âˆ¸â€¿cancel y x âŸ©
    (y âˆ™ x) âˆ¸ y â‰¡âŸ¨ cong (_âˆ¸ y) (comm y x) âŸ©
    (x âˆ™ y) âˆ¸ y â‰¡âŸ¨ cong (_âˆ¸ y) xâˆ™yâ‰¡Îµ âŸ©
    Îµ âˆ¸ y       â‰¡âŸ¨ Îµâˆ¸ y âŸ©
    Îµ âˆ

  antisym : Antisymmetric _â‰¤_
  antisym {x} {y} (kâ‚ , yâ‰¡xâˆ™kâ‚) (kâ‚‚ , xâ‰¡yâˆ™kâ‚‚) =
      x      â‰¡âŸ¨ xâ‰¡yâˆ™kâ‚‚ âŸ©
      y âˆ™ kâ‚‚ â‰¡âŸ¨ cong (y âˆ™_) (zeroSumFree kâ‚‚ kâ‚ (sym (cancelË¡ y Îµ (kâ‚‚ âˆ™ kâ‚) yâˆ™Îµâ‰¡yâˆ™âŸ¨kâ‚‚âˆ™kâ‚âŸ©))) âŸ©
      y âˆ™ Îµ  â‰¡âŸ¨ âˆ™Îµ y âŸ©
      y âˆ
    where
    yâˆ™Îµâ‰¡yâˆ™âŸ¨kâ‚‚âˆ™kâ‚âŸ© =
      y âˆ™ Îµ       â‰¡âŸ¨ âˆ™Îµ y âŸ©
      y           â‰¡âŸ¨ yâ‰¡xâˆ™kâ‚ âŸ©
      x âˆ™ kâ‚      â‰¡âŸ¨ cong (_âˆ™ kâ‚) xâ‰¡yâˆ™kâ‚‚ âŸ©
      y âˆ™ kâ‚‚ âˆ™ kâ‚ â‰¡âŸ¨ assoc y kâ‚‚ kâ‚ âŸ©
      y âˆ™ (kâ‚‚ âˆ™ kâ‚) âˆ

  partialOrder : PartialOrder _ _
  PartialOrder.preorder partialOrder = preorder
  PartialOrder.antisym partialOrder = antisym
