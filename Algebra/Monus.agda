{-# OPTIONS --cubical --safe #-}

module Algebra.Monus where

open import Prelude
open import Algebra
open import Relation.Binary
open import Path.Reasoning

-- Positively ordered monoids
record POM â„“ : Type (â„“suc â„“) where
  field commutativeMonoid : CommutativeMonoid â„“
  open CommutativeMonoid commutativeMonoid public
  field preorder : Preorder ğ‘† â„“
  open Preorder preorder public
  field
    positive : âˆ€ x â†’ Îµ â‰¤ x
    â‰¤-cong : âˆ€ x {y z} â†’ y â‰¤ z â†’ x âˆ™ y â‰¤ x âˆ™ z

  xâ‰¤xâˆ™y : âˆ€ {x y} â†’ x â‰¤ x âˆ™ y
  xâ‰¤xâˆ™y {x} {y} = subst (_â‰¤ x âˆ™ y) (âˆ™Îµ x) (â‰¤-cong x (positive y))

  â‰¤-congÊ³ : âˆ€ x {y z} â†’ y â‰¤ z â†’ y âˆ™ x â‰¤ z âˆ™ x
  â‰¤-congÊ³ x {y} {z} p = subst (y âˆ™ x â‰¤_) (comm x z) (subst (_â‰¤ x âˆ™ z) (comm x y) (â‰¤-cong x p))

-- Every commutative monoid generates a positively ordered monoid
-- called the "algebraic" pom
module AlgebraicPOM {â„“} (mon : CommutativeMonoid â„“) where
  commutativeMonoid = mon
  open CommutativeMonoid mon

  infix 4 _â‰¤_
  _â‰¤_ : ğ‘† â†’ ğ‘† â†’ Type _
  x â‰¤ y = âˆƒ[ z ] (y â‰¡ x âˆ™ z)

  â‰¤-trans : Transitive _â‰¤_
  â‰¤-trans (kâ‚ , _) (kâ‚‚ , _) .fst = kâ‚ âˆ™ kâ‚‚
  â‰¤-trans {x} {y} {z} (kâ‚ , yâ‰¡xâˆ™kâ‚) (kâ‚‚ , zâ‰¡yâˆ™kâ‚‚) .snd =
    z â‰¡âŸ¨ zâ‰¡yâˆ™kâ‚‚ âŸ©
    y âˆ™ kâ‚‚ â‰¡âŸ¨ cong (_âˆ™ kâ‚‚) yâ‰¡xâˆ™kâ‚ âŸ©
    (x âˆ™ kâ‚) âˆ™ kâ‚‚ â‰¡âŸ¨ assoc x kâ‚ kâ‚‚ âŸ©
    x âˆ™ (kâ‚ âˆ™ kâ‚‚) âˆ

  preorder : Preorder ğ‘† â„“
  Preorder._â‰¤_ preorder = _â‰¤_
  Preorder.refl preorder = Îµ , sym (âˆ™Îµ _)
  Preorder.trans preorder = â‰¤-trans

  positive : âˆ€ x â†’ Îµ â‰¤ x
  positive x = x , sym (Îµâˆ™ x)

  â‰¤-cong : âˆ€ x {y z} â†’ y â‰¤ z â†’ x âˆ™ y â‰¤ x âˆ™ z
  â‰¤-cong x (k , zâ‰¡yâˆ™k) = k , cong (x âˆ™_) zâ‰¡yâˆ™k Í¾ sym (assoc x _ k)

algebraic-pom : âˆ€ {â„“} â†’ CommutativeMonoid â„“ â†’ POM â„“
algebraic-pom mon = record { AlgebraicPOM mon }

-- Commutative Monoids with Monus
record CMM â„“ : Type (â„“suc â„“) where
  field commutativeMonoid : CommutativeMonoid â„“
  open CommutativeMonoid commutativeMonoid public

  field _âˆ¸_ : ğ‘† â†’ ğ‘† â†’ ğ‘†
  infixl 6 _âˆ¸_
  field
    âˆ¸â€¿comm  : âˆ€ x y â†’ x âˆ™ (y âˆ¸ x) â‰¡ y âˆ™ (x âˆ¸ y)
    âˆ¸â€¿assoc : âˆ€ x y z â†’ (x âˆ¸ y) âˆ¸ z â‰¡ x âˆ¸ (y âˆ™ z)
    âˆ¸â€¿inv   : âˆ€ x â†’ x âˆ¸ x â‰¡ Îµ
    Îµâˆ¸      : âˆ€ x â†’ Îµ âˆ¸ x â‰¡ Îµ

  open import Path.Reasoning

  âˆ¸Îµ : âˆ€ x â†’ x âˆ¸ Îµ â‰¡ x
  âˆ¸Îµ x =
    x âˆ¸ Îµ â‰¡Ë˜âŸ¨ Îµâˆ™ (x âˆ¸ Îµ) âŸ©
    Îµ âˆ™ (x âˆ¸ Îµ) â‰¡âŸ¨ âˆ¸â€¿comm Îµ x âŸ©
    x âˆ™ (Îµ âˆ¸ x) â‰¡âŸ¨ cong (x âˆ™_) (Îµâˆ¸ x) âŸ©
    x âˆ™ Îµ â‰¡âŸ¨ âˆ™Îµ x âŸ©
    x âˆ

-- Cancellative Commutative Monoids with Monus
record CCMM â„“ : Type (â„“suc â„“) where
  field cmm : CMM â„“
  open CMM cmm public

  field âˆ¸â€¿cancel : âˆ€ x y â†’ (x âˆ™ y) âˆ¸ x â‰¡ y

  open import Path.Reasoning

  cancelË¡ : CancellativeË¡ _âˆ™_
  cancelË¡ x y z p =
    y â‰¡Ë˜âŸ¨ âˆ¸â€¿cancel x y âŸ©
    (x âˆ™ y) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) p âŸ©
    (x âˆ™ z) âˆ¸ x â‰¡âŸ¨ âˆ¸â€¿cancel x z âŸ©
    z âˆ

  cancelÊ³ : CancellativeÊ³ _âˆ™_
  cancelÊ³ x y z p =
    y â‰¡Ë˜âŸ¨ âˆ¸â€¿cancel x y âŸ©
    (x âˆ™ y) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) (comm x y) âŸ©
    (y âˆ™ x) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) p âŸ©
    (z âˆ™ x) âˆ¸ x â‰¡âŸ¨ cong (_âˆ¸ x) (comm z x) âŸ©
    (x âˆ™ z) âˆ¸ x â‰¡âŸ¨ âˆ¸â€¿cancel x z âŸ©
    z âˆ

  pom : POM _
  pom = algebraic-pom commutativeMonoid

  open POM pom public hiding (semigroup; commutativeMonoid; monoid; _âˆ™_; Îµ; assoc; comm; Îµâˆ™; âˆ™Îµ)

  zeroSumFree : âˆ€ x y â†’ x âˆ™ y â‰¡ Îµ â†’ x â‰¡ Îµ
  zeroSumFree x y xâˆ™yâ‰¡Îµ = sym (âˆ¸â€¿cancel y x) Í¾ cong (_âˆ¸ y) (comm y x Í¾ xâˆ™yâ‰¡Îµ) Í¾ Îµâˆ¸ y

  antisym : Antisymmetric _â‰¤_
  antisym {x} {y} (kâ‚ , yâ‰¡xâˆ™kâ‚) (kâ‚‚ , xâ‰¡yâˆ™kâ‚‚) =
    sym (yâ‰¡xâˆ™kâ‚ Í¾ cong (x âˆ™_) (zeroSumFree kâ‚ kâ‚‚ (sym (sym (âˆ¸â€¿inv x) Í¾ cong (_âˆ¸ x) (xâ‰¡yâˆ™kâ‚‚ Í¾ cong (_âˆ™ kâ‚‚) yâ‰¡xâˆ™kâ‚ Í¾ assoc x kâ‚ kâ‚‚) Í¾ âˆ¸â€¿cancel x (kâ‚ âˆ™ kâ‚‚)))) Í¾ âˆ™Îµ x)

  partialOrder : PartialOrder _ _
  PartialOrder.preorder partialOrder = preorder
  PartialOrder.antisym partialOrder = antisym

record Monus â„“ : Type (â„“suc â„“) where
  field commutativeMonoid : CommutativeMonoid â„“

  pom : POM _
  pom = algebraic-pom commutativeMonoid

  open POM pom public hiding (refl)

  field
    _â‰¤|â‰¥_ : Total _â‰¤_
    antisym : Antisymmetric _â‰¤_

  zeroSumFree : âˆ€ x y â†’ x âˆ™ y â‰¡ Îµ â†’ x â‰¡ Îµ
  zeroSumFree x y xâˆ™yâ‰¡Îµ = antisym (y , sym xâˆ™yâ‰¡Îµ) (positive x)

  totalOrder : TotalOrder ğ‘† â„“ â„“
  totalOrder = fromPartialOrder (record { preorder = preorder ; antisym = antisym }) _â‰¤|â‰¥_


  open TotalOrder totalOrder
    hiding (refl; antisym; _â‰¤_; _â‰¤|â‰¥_; partialOrder; â‰¤-trans; _â‰¥_; _â‰°_; _â‰±_)
    public

  diffâ‰¢Îµ : âˆ€ {x y} â†’ (x<y : x < y) â†’ fst (<â‡’â‰¤ x<y) â‰¢ Îµ
  diffâ‰¢Îµ x<y with <â‡’â‰¤ x<y
  diffâ‰¢Îµ x<y | k , yâ‰¡xâˆ™k = Î» kâ‰¡Îµ â†’ irrefl (subst (_< _) (sym (yâ‰¡xâˆ™k Í¾ cong (_ âˆ™_) kâ‰¡Îµ Í¾ âˆ™Îµ _)) x<y)
